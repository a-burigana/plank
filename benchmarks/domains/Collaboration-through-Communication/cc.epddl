(define (domain collaboration-through-communication)
    (:requirements
        :typing :partial-observability :ontic-actions :conditional-effects
        :facts :lists :list-comprehensions
        :negative-preconditions :existential-preconditions :modal-preconditions
        :negative-obs-conditions :existential-obs-conditions
    )

    (:action-type-libraries intermediate)

    (:types box room)

    (:predicates
        (at-ag  ?i - agent ?r - room)
        (at-box ?b - box   ?r - room)
        ;; Facts
        (:fact neighbor  ?r1 ?r2 - room)
        (:fact leftmost  ?r      - room)
        (:fact rightmost ?r      - room)
    )

    (:event nil)

    ;--------------------LEFT------------------

    (:event e-left
        :parameters (?i - agent)
        :precondition
            (not
                (exists (?room-from - room)
                    (and (at-ag ?i ?room-from) (leftmost ?room-from)) ))
        :effects
            (:forall (?room-from - (either room room))
                (when (at-ag ?i ?room-from)
                    (:and
                        (not (at-ag ?i ?room-from))
                        (:forall (?room-to - room | (neighbor ?room-to ?room-from))
                            (at-ag ?i ?room-to) ))))
    )

    (:action left
        :parameters (?i - agent)
        :action-type (public-ontic (e-left ?i))
        :observability-conditions (default Fully)
    )

    ;--------------------RIGHT------------------

    (:event e-right
        :parameters (?i - agent)
        :precondition
            (not
                (exists (?room-from - room)
                    (and (at-ag ?i ?room-from) (rightmost ?room-from)) ))
        :effects
            (:forall (?room-from - room)
                (when (at-ag ?i ?room-from)
                    (:and
                        (not (at-ag ?i ?room-from))
                        (:forall (?room-to - room | (neighbor ?room-from ?room-to))
                            (at-ag ?i ?room-to) ))))
    )

    (:action right
        :parameters (?i - agent)
        :action-type (public-ontic (e-right ?i))
        :observability-conditions (default Fully)
    )

    ;--------------------SENSE------------------

    (:event e-sense-pos
        :parameters (?i - agent ?box - box ?room - room)
        :precondition (and (at-ag ?i ?room) (at-box ?box ?room))
    )

    (:event e-sense-neg
        :parameters (?i - agent ?box - box ?room - room)
        :precondition (and (at-ag ?i ?room) (not (at-box ?box ?room)))
    )

    (:action sense
        :parameters (?i - agent ?box - box ?room - room)
        :action-type (sensing (e-sense-pos ?i ?box ?room)
                              (e-sense-neg ?i ?box ?room)
                              (nil) )
        :observability-conditions
            (:and
                (?i Fully)
                (default Oblivious)
            )
    )

    ;--------------------TELL------------------

    (:event e-tell
        :parameters (?i - agent ?box - box ?room - room)
        :precondition (and ([?i] (at-box ?box ?room)) (at-box ?box ?room))
    )

    (:action tell
        :parameters (?i - agent ?box - box ?room - room)
        :action-type (private-announcement (e-tell ?i ?box ?room) (nil) )
        :observability-conditions
            (:forall (?j - agent)
                (?j
                    (if (exists (?r - room) (and (at-ag ?i ?r) (not (at-ag ?j ?r))))
                        Oblivious
                    else
                        Fully
                    )
                )
            )
    )
)
