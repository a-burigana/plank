(define (library ma_star)
    (:requirements :del)

    (:observability-groups Fully Partially Oblivious)

    (:event sigma_ont
        :parameters (?pre   - formula
                     ?ps    - [predicate]
                     ?posts - [formula])
        :precondition (?pre)
        :postconditions ((?ps) when (?posts))
    )

    (:event sigma_sen
        :parameters (?pre - formula
                     ?p   - predicate)
        :precondition (and (?p) (?pre))
    )

    (:event tau_sen
        :parameters (?pre - formula
                     ?p   - predicate)
        :precondition (and (not (?p)) (?pre))
    )

    (:event sigma_ann
        :parameters (?pre - formula
                     ?pf  - predicate-formula)
        :precondition (and (?pf) (?pre))
    )

    (:event tau_ann
        :parameters (?pre - formula
                     ?pf  - predicate-formula)
        :precondition (and (not (?pf)) (?pre))
    )

    (:event eps
        :parameters ()
        :precondition ()
    )

    (:action-type ontic
        :parameters (?pre   - formula
                     ?ps    - [predicate]
                     ?posts - [formula])
        :frame-of-reference (Fully Oblivious)
        :events     (sigma_ont ?pre ?ps ?posts)
                    (eps)
        :relations  (Fully     (sigma_ont sigma_ont))
                    (Oblivious (sigma_ont eps))
                    (All       (eps       eps))
        :designated (sigma_ont)
    )

    (:action-type sensing
        :parameters (?pre - formula
                     ?p   - predicate)
        :frame-of-reference (Fully Partially Oblivious)
        :events     (sigma_sen ?pre ?p)
                    (tau_sen   ?pre ?p)
                    (eps)
        :relations  (Fully     (sigma_sen sigma_sen) (tau_sen tau_sen))
                    (Partially (sigma_sen sigma_sen) (tau_sen tau_sen) (sigma_sen tau_sen) (tau_sen sigma_sen))
                    (Oblivious (sigma_sen eps      ) (tau_sen eps    ))
                    (All       (eps eps))
        :designated (sigma_sen tau_sen)
    )

    (:action-type announcement
        :parameters (?pre - formula
                     ?pf  - predicate-formula)
        :frame-of-reference (Fully Partially Oblivious)
        :events     (sigma_ann ?pre ?pf)
                    (tau_ann   ?pre ?pf)
                    (eps)
        :relations  (Fully     (sigma_ann sigma_ann) (tau_ann tau_ann))
                    (Partially (sigma_ann sigma_ann) (tau_ann tau_ann) (sigma_ann tau_ann) (tau_ann sigma_ann))
                    (Oblivious (sigma_ann eps      ) (tau_ann eps    ))
                    (All       (eps eps))
        :designated (sigma_ann)
    )
)
