(define (domain assemble)
    (:requirements :del :typing :ontic-change
                   :modal-preconditions
    )

    (:action-type-libraries ma_star)

    (:predicates (assembled)
                 (restart)
                 (qualify ?i - agent)
    )

    (:modalities kw.)

    (:action assemble
        :parameters (?ag1 ?ag2 - agent)
        :where (not (= ?ag1 ?ag2))
        :action-type (ontic
                        (?post :: (set (assembled)))
                     )
        :precondition (and [?ag1][?ag2](qualify ?ag2)
                           [?ag2][?ag1](qualify ?ag1))
        :observability-conditions (All Fully)
    )

    (:action restart
        :parameters (?ag1 ?ag2 - agent)
        :where (not (= ?ag1 ?ag2))
        :action-type (ontic
                        (?post :: (set (restart)))
                     )
        :precondition (and [?ag1][?ag2](not (qualify ?ag1))
                           [?ag2][?ag1](not (qualify ?ag2)))
        :observability-conditions (All Fully)
    )

    (:action sense
        :parameters (?ag - agent)
        :action-type (sensing
                        (?sensed :: (qualify ?ag))
                     )
        :precondition (not [kw. ?ag](qualify ?ag))
        :observability-conditions (?ag Fully)
                                  (?_ Oblivious)
    )

    (:action tell
        :parameters (?ag - agent)
        :action-type (announcement
                        (?announced :: (qualify ?ag))
                     )
        :precondition ([?ag](qualify ?ag))
        :observability-conditions (All Fully)
    )
)
