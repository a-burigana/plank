(define (problem cc_2_2_3-6)          ;; Plan length: 3
    (:domain collaboration-through-communication)

    (:requirements :finitary-S5-theories)

    (:agents A B)

    (:objects room1 room2 room3 - room
              box1 box2 - box
    )

    (:facts-init
        (neighbor room1 room2)
        (neighbor room2 room3)
        (leftmost room1)
        (rightmost room3)
    )

    (:init
        (:and
            ;; Agents positions
            (at-ag A    room2) (not (at-ag A    room1)) (not (at-ag A    room3))
            (at-ag B    room2) (not (at-ag B    room1)) (not (at-ag B    room3))
            ;; Boxes positions
            (at-box box1 room1) (not (at-box box1 room2)) (not (at-box box1 room3))
            (at-box box2 room3) (not (at-box box2 room1)) (not (at-box box2 room2))
            ;; CK of agents positions
            ([C. All] (and (at-ag A room2) (not (at-ag A room1)) (not (at-ag A room3))))
            ([C. All] (and (at-ag B room2) (not (at-ag B room1)) (not (at-ag B room3))))
            ;; CK that-ag each box is in only one room
            ([C. All] (or (and (at-box box1 room1) (not (at-box box1 room3)))
                      (and (at-box box1 room3) (not (at-box box1 room1)))))
            ([C. All] (or (and (at-box box2 room1) (not (at-box box2 room3)))
                      (and (at-box box2 room3) (not (at-box box2 room1)))))
        )
    )

    (:goal
        (and
            ([A] (and (at-box box1 room1) (at-box box2 room3)))
            ([B] (and (at-box box1 room1) (at-box box2 room3)))
            ([A] (and (not ([B] (at-box box1 room1))) (not ([B] (at-box box2 room3)))))
            ([B] (and (not ([A] (at-box box1 room1))) (not ([A] (at-box box2 room3)))))
            ([A] (at-ag A room3))
            ([B] (at-ag A room3)) )
    )
)
