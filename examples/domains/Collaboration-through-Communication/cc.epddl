(define (domain collaboration-through-communication)
    (:requirements
        :typing :partial-observability :ontic-actions :conditional-effects
        :static-predicates :lists :list-comprehensions
        :negative-preconditions :existential-preconditions :modal-preconditions
        :negative-obs-conditions :existential-obs-conditions
    )

    (:action-type-libraries ma-star)    ; todo: fix wrong library name ma-starz

    (:types box room)

    (:predicates
        (at-ag  ?i - agent ?r - room)
        (at-box ?b - box   ?r - room)
        ;; Static predicates
        (:static neighbor  ?r1 ?r2 - room)
        (:static leftmost  ?r      - room)
        (:static rightmost ?r      - room)
    )

    (:event null)

    ;--------------------LEFT------------------

    (:event e-left
        :parameters (?i - agent)
        :precondition
            (not
                (exists (?room_from - room)
                    (and (at-ag ?i ?room_from) (leftmost ?room_from)) ))
        :effects
            (:forall (?room_from - (either room room))
                (when (at-ag ?i ?room_from)
                    (:and
                        (not (at-ag ?i ?room_from))
                        (:forall (?room_to - room | (neighbor ?room_to ?room_from))
                            (at-ag ?i ?room_to) ))))
    )

    (:action left
        :parameters (?i - agent)
        :action-type (ontic (e-left ?i) (null))
        :observability-conditions (default Fully)
    )

    ;--------------------RIGHT------------------

    (:event e-right
        :parameters (?i - agent)
        :precondition
            (not
                (exists (?room_from - room)
                    (and (at-ag ?i ?room_from) (rightmost ?room_from)) ))
        :effects
            (:forall (?room_from - room)
                (when (at-ag ?i ?room_from)
                    (:and
                        (not (at-ag ?i ?room_from))
                        (:forall (?room_to - room | (neighbor ?room_from ?room_to))
                            (at-ag ?i ?room_to) ))))
    )

    (:action right
        :parameters (?i - agent)
        :action-type (ontic (e-right ?i) (null))
        :observability-conditions (default Fully)
    )

    ;--------------------SENSE------------------

    (:event e-sense-pos
        :parameters (?i - agent ?box - box ?room - room)
        :precondition (and (at-ag ?i ?room) (at-box ?box ?room))
    )

    (:event e-sense-neg
        :parameters (?i - agent ?box - box ?room - room)
        :precondition (and (at-ag ?i ?room) (not (at-box ?box ?room)))
    )

    (:action sense
        :parameters (?i - agent ?box - box ?room - room)
        :action-type (sensing (e-sense-pos ?i ?box ?room)
                              (e-sense-neg ?i ?box ?room)
                              (null) )
        :observability-conditions
            (:and
                (Fully ?i)
                (default Oblivious)
            )
    )

    ;--------------------TELL------------------

    (:event e-tell-pos
        :parameters (?i - agent ?box - box ?room - room)
        :precondition (and ([?i] (at-box ?box ?room)) (at-box ?box ?room))
    )

    (:event e-tell-neg
        :parameters (?i - agent ?box - box ?room - room)
        :precondition (and ([?i] (not (at-box ?box ?room))) (not (at-box ?box ?room)))
    )

    (:action tell
        :parameters (?i - agent ?box - box ?room - room)
        :action-type (announcement (e-tell-pos ?i ?box ?room)
                                   (e-tell-neg ?i ?box ?room)
                                   (null) )
        :observability-conditions
            (:forall (?j - agent)
                (if (exists (?r - room) (and (at-ag ?i ?r) (not (at-ag ?j ?r))))
                    (Oblivious ?j)
                else
                    (Fully ?j)
                )
            )
    )
)
