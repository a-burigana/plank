(define (domain collaboration-through-communication)
    (:requirements :del :ontic-actions :modal-preconditions)

    (:action-type-libraries ma-star)

    (:types box room)

    (:predicates (at_ag  ?i - agent ?r - room)
                 (at_box ?b - box   ?r - room)
                 ;; Static predicates
                 (:static neighbor  ?r1 ?r2 - room)
                 (:static leftmost  ?r      - room)
                 (:static rightmost ?r      - room)
    )

    ;--------------------EVENTS------------------

    (:event e_left
        :parameters (?i - agent)
        :precondition (not (exists (?room_from - room)
                        (and (at_ag ?i ?room_from) (leftmost ?room_from))
                        )
                      )
        :effects (:forall
                   (?room_from - room)
                    (when (at_ag ?i ?room_from)
                        (:and
                            (not (at_ag ?i ?room_from))
                            (:forall
                                (?room_to - room | (neighbor ?room_to ?room_from))
                                (at_ag ?i ?room_to)
                            )
                        )
                    )
                )
    )

    (:event e_right
        :parameters (?i - agent)
        :precondition (not (exists (?room_from - room)
                        (and (at_ag ?i ?room_from) (rightmost ?room_from))
                      ))
        :effects (:forall
                   (?room_from - room)
                    (when (at_ag ?i ?room_from)
                        (:and
                            (not (at_ag ?i ?room_from))
                            (:forall
                                (?room_to - room | (neighbor ?room_from ?room_to))
                                (at_ag ?i ?room_to)
                            )
                        )
                    )
                )
    )


    (:event e_sense
        :parameters (?i - agent ?box - box ?room - room)
        :precondition (and (at_ag ?i ?room) (at_box ?box ?room))
        ;   :effects ()
    )

    (:event e_sense_neg
        :parameters (?i - agent ?box - box ?room - room)
        :precondition (and (at_ag ?i ?room) (not (at_box ?box ?room)))
        ;   :effects ()
    )

     (:event e_tell_box_here
        :parameters (?i - agent ?box - box ?room - room)
        :precondition (and ([?i] (at_box ?box ?room)) (at_box ?box ?room))
        ;   :effects ()
     )

     (:event e_tell_box_not_here
        :parameters (?i - agent ?box - box ?room - room)
        :precondition (and ([?i] (not (at_box ?box ?room))) (not (at_box ?box ?room)))
        ;   :effects ()
     )

    (:event epsilon
        ;   :parameters ()
        ;   :precondition ()
        ;   :effects ()
    )

    ;--------------------ACTIONS------------------

    (:action left
        :parameters (?i - agent)
        :action-type (ontic
                           (e_left ?i)
                           (epsilon) )
        :observability-conditions (default Fully)
    )

    (:action right
        :parameters (?i - agent)
        :action-type (ontic
                           (e_right ?i)
                           (epsilon) )
        :observability-conditions (default Fully)
    )

    (:action sense
        :parameters (?i - agent ?box - box ?room - room)
        :action-type (sensing (e_sense      ?i ?box ?room)
                              (e_sense_neg  ?i ?box ?room)
                              (epsilon))
        :observability-conditions
            (:and
                (Fully ?i)
                (default Oblivious)
            )
    )

     (:action tell_box
        :parameters (?i - agent ?box - box ?room - room)
        :action-type (announcement (e_tell_box_here      ?i ?box ?room)
                              (e_tell_box_not_here  ?i ?box ?room)
                              (epsilon))
        :observability-conditions
            (:and
                (Fully ?i)
                (default Oblivious)
            )
    )
)
