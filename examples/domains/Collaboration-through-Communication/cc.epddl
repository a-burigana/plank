(define (domain collaboration-through-communication)
    (:requirements :del :ontic-actions :modal-preconditions)

    (:action-type-libraries ma-star)

    (:types box room)

    (:predicates (at ?i - agent ?r - room)
                 (at ?b - box   ?r - room)
    )
    
    (:action left
        :parameters (?i - agent)
        :action-type (ontic)
        :precondition (e1
            (forall (?room_from - room | (at ?i ?room_from))
                (not (leftmost ?room_from))
            ))
        :effects (e1
            (forall (?room_from - room | (at ?i ?room_from))
                (and
                    (not (at ?i ?room_from))
                    (forall (?room_to - room | (neighbor ?room_to ?room_from))
                        (at ?i ?room_to)
                    )
                )))
        :observability-conditions (always All Fully)
    )

    (:action right
        :parameters (?i - agent)
        :action-type (ontic)
        :precondition (e1
            (forall (?room_from - room | (at ?i ?room_from))
                (not (rightmost ?room_from))
            ))
        :effects (e1
            (forall (?room_from - room | (at ?i ?room_from))
                (and
                    (not (at ?i ?room_from))
                    (forall (?room_to - room | (neighbor ?room_from ?room_to))
                        (at ?i ?room_to)
                    )
                )))
        :observability-conditions (always All Fully)
    )

    (:action sense
        :parameters (?i - agent ?box - box ?room - room)
        :action-type (sensing)
        :precondition
            (e1 (and (at ?i ?room) (at ?box ?room))
             e2 (and (at ?i ?room) (not (at ?box ?room))))
        :effects ()
        :observability-conditions
            (and
                (always ?i Fully)
                (default Oblivious)
            )
    )

    (:action tell
        :parameters (?i - agent ?box - box ?room - room)
        :action-type (announcement)
        :precondition (e1 (and ([?i] (at ?box ?room)) (at ?box ?room)))
        :effects ()
        :observability-conditions
            (and
                (always ?i Fully)
                (forall (?j - agent | (exists (?room2 - room) (and (at ?i ?room2) (at ?j ?room2))))
                    (always ?j Fully))
                (default Oblivious)
            )
    )
)
