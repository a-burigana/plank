(define (problem cn-5)
    (:domain consecutive-numbers)

    (:requirements :typing :equality :list-comprehensions :facts :finitary-S5-theories :modal-goals)

    (:objects
        n0 n2 n4 - even
        n1 n3 n5 - odd
    )

    (:agents A B)

    (:facts-init
        (consecutive n0 n1) (consecutive n1 n0)
        (consecutive n1 n2) (consecutive n2 n1)
        (consecutive n2 n3) (consecutive n3 n2)
        (consecutive n3 n4) (consecutive n4 n3)
        (consecutive n4 n5) (consecutive n5 n4)
    )

    (:init
        (:and
            ; Actual configurations
            (or (has A n3) (has A n5)) (has B n4)
            ; A has an odd number, B has an even number and the numbers are consecutive
            ([C. All]
                (exists (?m - odd ?n - even | (consecutive ?m ?n))
                    (and (has A ?m) (has B ?n))
                ))
            ; Agents have at most one number
            ([C. All]
                (forall (?i - agent ?m ?n - number | (/= ?m ?n))
                    (imply (has ?i ?m) (not (has ?i ?n)))
                ))
            ; Agents know their number
            (:forall (?i - agent ?n - number)
                ([C. All]
                    ([Kw. ?i] (has ?i ?n)) ))
        ))

    (:goal
        ([B] ([A] (has B n4)))
    )
)
