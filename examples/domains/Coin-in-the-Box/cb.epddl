(define (domain coin-in-the-box)
    (:requirements :del :ontic-actions :modal-preconditions)

    (:action-type-libraries ma-star)

    (:predicates (tails) (opened) (looking ?i - agent) (has_key ?i - agent))


    ;--------------------EVENTS------------------

    (:event e_open
        :parameters (?ag - agent)
        :precondition (and (not (opened)) (has_key ?ag))
        :effects (opened)
    )

    (:event e_signal
        :parameters (?ag - agent)
        :precondition (not (looking ?ag))
        :effects (looking ?ag)
    )

    (:event e_distract
        :parameters (?ag - agent)
        :precondition (looking ?ag)
        :effects (not (looking ?ag))
    )

    (:event e_peek
        ;   :parameters ()
        :precondition (and (opened) (tails))
        ;   :effects ()
    )

     (:event e_peek_neg
        ;   :parameters ()
        :precondition (and (opened) (not (tails)))
        ;   :effects ()
     )

     (:event e_shout_tails
        :parameters (?ag - agent)
        :precondition ([?ag] (tails))
        ;   :effects ()
     )

     (:event e_shout_heads
        :parameters (?ag - agent)
        :precondition ([?ag] (not (tails)))
        ;   :effects ()
     )

    (:event epsilon
        ;   :parameters ()
        ;   :precondition ()
        ;   :effects ()
    )


    ;--------------------ACTIONS------------------

    (:action open
        :parameters   (?i - agent)
        :action-type  (ontic
                           (e_open ?i)
                           (epsilon) )
        :observability-conditions
            (:and
                (Fully ?i)
                (:forall (?j - agent)
                    (if (looking ?j) Fully ?j
                     else            Oblivious ?j)
                )
            )
    )

    (:action peek
        :parameters   (?i - agent)
        :action-type  (sensing (e_peek)
                               (e_peek_neg)
                               (epsilon))
        :observability-conditions
            (:and
                (Fully ?i)
                (:forall (?j - agent)
                    (if (looking ?j) Partially ?j
                     else            Oblivious ?j)
                )
            )
    )

    (:action shout_tails
        :parameters   (?i - agent)
        :action-type  (announcement (e_shout_tails ?i)
                                    (e_shout_heads ?i)
                                    (epsilon))
        :observability-conditions
            (:and
                (Fully ?i)
                (:forall (?j - agent)
                    (if (looking ?j) Partially ?j
                     else            Oblivious ?j)
                )
            )
    )

    (:action shout_heads
        :parameters   (?i - agent)
        :action-type  (announcement (e_shout_heads ?i)
                                    (e_shout_tails ?i)
                                    (epsilon))
        :observability-conditions
            (:and
                (Fully ?i)
                (:forall (?j - agent)
                    (if (looking ?j) Partially ?j
                     else            Oblivious ?j)
                )
            )
    )

    (:action signal
        :parameters   (?i ?j - agent)
        :action-type  (ontic (e_signal ?j)
                             (e_distract ?j)
                             (epsilon))
        :observability-conditions
            (:and
                (Fully ?i)
                (Fully ?j)
                (default Oblivious)
            )
    )

    (:action distract
        :parameters   (?i ?j - agent)
        :action-type  (ontic (e_distract ?j)
                             (e_signal ?j)
                             (epsilon))
        :observability-conditions
            (:and
                (Fully ?i)
                (Fully ?j)
                (default Oblivious)
            )
    )
)
