(define (domain cb)
    (:requirements :del :multi-agent :ontic-actions
                   :modal-preconditions :modal-postconditions :common-knowledge
    )

    (:action-type-libraries ma_star)

    (:predicates (tails)
                 (opened)
                 (looking ?i - agent)
                 (has_key ?i - agent)
    )

    (:action open
        :parameters (?ag - agent)
        :precondition (and (has_key ?ag) [?ag](not (opened)))
        :action-type (ontic
                        (~effects (opened))
                     )
        :observability-conditions
            (and
                (always ?ag Fully)
                (forall (?ag2 - agent)
                    (if (looking ?ag2) ?ag2 Fully)
                    (else              ?ag2 Oblivious)
                )
            )
    )

    (:action peek
        :parameters (?ag - agent)
        :precondition [?ag](opened)
        :action-type (sensing
                        (~predicate (tails))
                     )
        :observability-conditions
            (and
                (always ?ag Fully)
                (forall (?ag2 - agent)
                    (if (looking ?ag2) ?ag2 Partially)
                    (else              ?ag2 Oblivious)
                )
            )
    )

    (:action shout
        :parameters (?ag - agent)
        :precondition [?ag](tails)
        :action-type (announcement
                        (~formula (tails))
                     )
        :observability-conditions
            (and
                (always ?ag Fully)
                (forall (?ag2 - agent)
                    (if (looking ?ag2) ?ag2 Partially)
                    (else              ?ag2 Oblivious)
                )
            )
    )

    (:action signal
        :parameters (?ag1 - agent ?ag2 - agent)
        :precondition (and (looking ?ag1) [?ag1](not (looking ?ag2)))
        :action-type (ontic
                        (~effects (looking ?ag2))
                     )
        :observability-conditions
            (and
                (always ?ag1 Fully)
                (always ?ag2 Fully)
                (forall (?ag - agent)
                    (always ?ag Oblivious)
                )
            )
    )

    (:action distract
        :parameters (?ag1 - agent ?ag2 - agent)
        :precondition (and (looking ?ag1) [?ag1](not (looking ?ag2)))
        :action-type (ontic
                        (~effects (not (looking ?ag2)))
                     )
        :observability-conditions
            (and
                (always ?ag1 Fully)
                (always ?ag2 Fully)
                (forall (?ag - agent)
                    (always ?ag Oblivious)
                )
            )
    )
)
