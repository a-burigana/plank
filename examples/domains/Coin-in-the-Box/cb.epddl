(define (domain coin-in-the-box)
    (:requirements :del :ontic-actions :modal-preconditions)

    (:action-type-libraries ma-star)

    (:predicates (tails) (opened) (looking ?i - agent) (has_key ?i - agent))


    (:event e_open
        :parameters (?ag - agent)
        :precondition (and (not (opened)) (has_key ?ag))
        :effects (opened)
    )

    (:event epsilon
    )

    (:action open
        :parameters   (?i - agent)
        :action-type  (ontic (e_open ?i epsilon))
        :observability-conditions
            (and
                (always ?i Fully)
                (forall (?j - agent)
                    (if (looking ?j) ?j Fully
                     else            ?j Oblivious)
                )
            )
    )

    (:action peek
        :parameters   (?i - agent)
        :action-type  (sensing)
        :precondition (e1 (and (tails)       ([?i] (opened)))
                       e2 (and (not (tails)) ([?i] (opened))))
        :effects      ()
        :observability-conditions
            (and
                (always ?i Fully)
                (forall (?j - agent)
                    (if (looking ?j) ?j Partially
                     else            ?j Oblivious)
                )
            )
    )

    (:action shout
        :parameters   (?i - agent)
        :action-type  (announcement)
        :precondition (e1 (and (tails)       ([?i] (tails)))
                       e2 (and (not (tails)) ([?i] (tails))))
        :effects ()
        :observability-conditions
            (and
                (always ?i Fully)
                (forall (?j - agent)
                    (if (looking ?j) ?j Partially
                     else            ?j Oblivious)
                )
            )
    )

    (:action signal
        :parameters   (?i - agent ?j - agent)
        :action-type  (ontic)
        :precondition (e1 (and (looking ?i) ([?i] (not (looking ?j)))))
        :effects      (e1 (looking ?j))
        :observability-conditions
            (and
                (always ?i Fully)
                (always ?j Fully)
                (default Oblivious)
            )
    )

    (:action distract
        :parameters   (?i - agent ?j - agent)
        :action-type  (ontic)
        :precondition (e1 (and (looking ?i) ([?i] (not (looking ?j)))))
        :effects      (e1 (not (looking ?j)))
        :observability-conditions
            (and
                (always ?i Fully)
                (always ?j Fully)
                (default Oblivious)
            )
    )
)
