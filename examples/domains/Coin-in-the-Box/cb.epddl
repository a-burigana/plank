(define (domain cb)
    (:requirements :del :multi-agent :ontic-actions :modal-preconditions)

    (:action-type-libraries ma-star)

    (:predicates (tails) (opened) (looking ?i - agent) (has_key ?i - agent))

    (:action open
        :parameters   (?ag - agent)
        :action-type  (ontic)
        :precondition (e1 (and (has_key ?ag) ([?ag] (not (opened)))))
        :effects      (e1 (opened))
        :observability-conditions
            (and
                (always ?ag Fully)      ; todo: find alternative for 'always'
                (forall (?ag2 - agent)
                    (if (looking ?ag2) ?ag2 Fully
                     else              ?ag2 Oblivious)
                )
            )
    )

    (:action peek
        :parameters (?ag - agent)
        :action-type (sensing)
        :precondition (e1 (and (tails)       ([?ag] (opened)))
                       e2 (and (not (tails)) ([?ag] (opened))))
        :effects ()
        :observability-conditions
            (and
                (always ?ag Fully)
                (forall (?ag2 - agent)
                    (if (looking ?ag2) ?ag2 Partially
                     else              ?ag2 Oblivious)
                )
            )
    )

    (:action shout
        :parameters (?ag - agent)
        :action-type (announcement)
        :precondition (e1 (and (tails)       ([?ag] (tails)))
                       e2 (and (not (tails)) ([?ag] (tails))))
        :effects ()
        :observability-conditions
            (and
                (always ?ag Fully)
                (forall (?ag2 - agent)
                    (if (looking ?ag2) ?ag2 Partially
                     else              ?ag2 Oblivious)
                )
            )
    )

    (:action signal
        :parameters (?ag1 - agent ?ag2 - agent)
        :action-type (ontic)
        :precondition (e1 (and (looking ?ag1) ([?ag1] (not (looking ?ag2)))))
        :effects      (e1 (looking ?ag2))
        :observability-conditions
            (and
                (always ?ag1 Fully)
                (always ?ag2 Fully)
                (default Oblivious)
            )
    )

    (:action distract
        :parameters (?ag1 - agent ?ag2 - agent)
        :action-type (ontic)
        :precondition (e1 (and (looking ?ag1) ([?ag1] (not (looking ?ag2)))))
        :effects      (e1 (not (looking ?ag2)))
        :observability-conditions
            (and
                (always ?ag1 Fully)
                (always ?ag2 Fully)
                (default Oblivious)
            )
    )
)
