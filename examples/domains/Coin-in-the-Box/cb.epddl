(define (domain cb)
    (:action-type-libraries ma_star)

    (:requirements :del :typing :ontic-change
                   :modal-preconditions
    )

    (:predicates (tails)
                 (opened)
                 (looking ?i - agent)
                 (has_key ?i - agent)
    )

    (:action open
        :parameters (?ag - agent)
        :action-type (ontic
                        (?post <- (opened))
                     )
        :precondition (and (has_key ?ag) [?ag](not (opened)))
        :observability-conditions
            (?ag Fully)
            (forall (?ag2 - agent)
                (if (looking ?ag2) ?ag2 Fully)
                (otherwise         ?ag2 Oblivious)
            )
    )

    (:action peek
        :parameters (?ag - agent)
        :action-type (sensing
                        (?sensed <- (tails))
                     )
        :precondition ([?ag](opened))
        :observability-conditions
            (?ag Fully)
            (forall (?ag2 - agent)
                (if (looking ?ag2) ?ag2 Partially)
                (otherwise         ?ag2 Oblivious)
            )
    )

    (:action signal
        :parameters (?ag1 ?ag2 - agent)
        :action-type (ontic
                        (?post <- (looking ?ag2))
                     )
        :precondition (and (looking ?ag1) [?ag1](not (looking ?ag2)))
        :observability-conditions
            (?ag1 Fully)
            (?ag2 Fully)
            (forall (?ag - agent)
                (?ag Oblivious)
            )
    )

    (:action distract
        :parameters (?ag1 ?ag2 - agent)
        :action-type (ontic
                        (?post <- (not (looking ?ag2)))
                     )
        :precondition (and (looking ?ag1) [?ag1](not (looking ?ag2)))
        :observability-conditions
            (?ag1 Fully)
            (?ag2 Fully)
            (forall (?ag - agent)
                (?ag Oblivious)
            )
    )

    (:action shout
        :parameters (?ag - agent)
        :action-type (announcement
                        (?announced <- (tails))
                     )
        :precondition ([?ag](tails))
        :observability-conditions
            (?ag Fully)
            (forall (?ag2 - agent)
                (if (looking ?ag2) ?ag2 Partially)
                (otherwise         ?ag2 Oblivious)
            )
    )
)
