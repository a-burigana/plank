(define (domain coin-in-the-box)
    (:requirements
        :equality :partial-observability :list-comprehensions :lists
        :ontic-actions :negative-preconditions :modal-preconditions
    )

    (:action-type-libraries ma-star)

    (:predicates
        (tails)
        (opened)
        (looking ?i - agent)
        (has-key ?i - agent)
    )

    (:event nil)

    ;--------------------OPEN------------------

    (:event e-open
        :parameters (?i - agent)
        :precondition (and (not (opened)) (looking ?i) (has-key ?i))
        :effects (opened)
    )

    (:action open
        :parameters (?i - agent)
        :action-type (ontic (e-open ?i) (nil))
        :observability-conditions
            (:and
                (?i Fully)
                (:forall (?j - agent | (/= ?i ?j))
                    (?j
                        (if (looking ?j)
                           Fully
                        ; else
                        ;    Oblivious
                        )
                    )
                )
                (default Oblivious)
            )
    )

    ;--------------------SIGNAL------------------

    (:event e-signal
        :parameters (?i - agent)
        :precondition (not (looking ?i))
        :effects (looking ?i)
    )

    (:action signal
        :parameters (?i ?j - agent | (/= ?i ?j))
        :action-type (ontic (e-signal ?j) (nil))
        :observability-conditions
            (:and
                (?i Fully)
                (?j Fully)
                (default Oblivious)
            )
    )

    ;--------------------DISTRACT------------------

    (:event e-distract
        :parameters (?i - agent)
        :precondition (looking ?i)
        :effects (not (looking ?i))
    )

    (:action distract
        :parameters   (?i ?j - agent | (/= ?i ?j))
        :action-type  (ontic (e-distract ?j) (nil))
        :observability-conditions
            (:and
                (?i Fully)
                (?j Fully)
                (default Oblivious)
            )
    )

    ;--------------------PEEK------------------

    (:event e-peek-pos
        :precondition (and (opened) (tails))
    )

     (:event e-peek-neg
        :precondition (and (opened) (not (tails)))
     )

     (:action peek
         :parameters (?i - agent)
         :action-type (sensing (e-peek-pos) (e-peek-neg) (nil))
         :observability-conditions
             (:and
                 (?i Fully)
                 (:forall (?j - agent | (/= ?i ?j))
                     (?j
                         (if (looking ?j)
                             Partially
                         else
                             Oblivious
                         )
                     )
                 )
             )
     )

    ;--------------------SHOUT-TAILS------------------

     (:event e-shout-tails
        :parameters (?i - agent)
        :precondition ([?i] (tails))
        ;   :effects ()
     )

     (:event e-shout-heads
        :parameters (?i - agent)
        :precondition ([?i] (not (tails)))
     )

    (:action shout-tails
        :parameters   (?i - agent)
        :action-type  (announcement (e-shout-tails ?i)
                                    (e-shout-heads ?i)
                                    (nil))
        :observability-conditions
            (:and
                (?i Fully)
                (:forall (?j - agent | (/= ?i ?j))
                    (?j
                        (if (looking ?j)
                            Partially
                        else
                            Oblivious
                        )
                    )
                )
            )
    )

    (:action shout-heads
        :parameters   (?i - agent)
        :action-type  (announcement (e-shout-heads ?i)
                                    (e-shout-tails ?i)
                                    (nil))
        :observability-conditions
            (:and
                (?i Fully)
                (:forall (?j - agent | (/= ?i ?j))
                     (?j
                         (if (looking ?j)
                             Partially
                         else
                             Oblivious
                         )
                     )
                 )
            )
    )
)
