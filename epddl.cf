---------- LBNF Grammar for EPDDL ----------
--
-- Authors: Alessandro Burigana, Francesco Fabiano
-- License: MIT
--


-- EPDDL Comments
comment                 ";" ;


-- Parsing Entrypoints
entrypoints             MainDef ;

-- Using MainDef because multiple entrypoints are not supported by BNFC cpp backend
-- Domain, action-type library and problem definitions MUST come in different files
Domain.                 MainDef                 ::= DomainDef ;
ActionTypeLibrary.      MainDef                 ::= LibraryDef ;
Problem.                MainDef                 ::= ProblemDef ;



----- EPDDL Domain -----
EPDDLDomain.            DomainDef               ::= "(" "define" "(" "domain" DomainName ")" [DomainItemDef] ")" ;

EPDDLDomLibrary.        DomainItemDef           ::= DomainLibrariesNameDef ;
EPDDLDomRequire.        DomainItemDef           ::= RequireDef ;
EPDDLDomTypes.          DomainItemDef           ::= TypesDef ;
EPDDLDomPredicates.     DomainItemDef           ::= PredicateListDef ;
EPDDLDomModalities.     DomainItemDef           ::= ModalitiesDef ;

EPDDLDomObsGroups.      DomainItemDef           ::= ObservabilityGroupsDef ;
EPDDLDomAction.         DomainItemDef           ::= ActionDef ;
separator               DomainItemDef "" ;


--- EPDDL Domain Library Definition
EPDDLDomLibraryNames.   DomainLibrariesNameDef  ::= "(" ":action-type-libraries" [LibraryName] ")" ;


--- EPDDL Requirements Definition
EPDDLRequire.           RequireDef              ::= "(" ":requirements" [RequireKey] ")" ;


--- EPDDL Types Definition
EPDDLTypes.             TypesDef                ::= "(" ":types" TypedIdentList ")" ;


--- EPDDL Predicates Definition
EPDDLPredicateList.     PredicateListDef        ::= "(" ":predicates" [PredicateDef] ")" ;
EPDDLPredicateDef.      PredicateDef            ::= "(" PredicateName TypedVariableList ")" ;
separator nonempty      PredicateDef "" ;


--- EPDDL Modalities Definition
EPDDLModalities.        ModalitiesDef           ::= "(" ":modalities" [ModalityName] ")" ;


--- EPDDL Observability Groups Definition
EPDDLObsGroupsNames.    ObservabilityGroupsDef  ::= "(" ":observability-groups" [ObservingAgentGroup] ")" ;


--- EPDDL Actions Definition
EPDDLAction.            ActionDef               ::= "(" ":action" ActionName ActionParameterDef ActionConditionsDef ActionTypeSignatureDef ActionPreDef ActionObsDef ")" ;

ActionParam.            ActionParameterDef      ::= ":parameters" "(" TypedVariableList ")" ;

ActionCond.             ActionConditionsDef     ::= ":where" ConditionFormula ;
EmptyActionCond.        ActionConditionsDef     ::= ;

ActionSignature.        ActionTypeSignatureDef  ::= ":action-type" "(" ActionTypeName [Parameter] ")" ;

ActionPre.              ActionPreDef            ::= ":precondition" FormulaOrEmpty ;

ActionObs.              ActionObsDef            ::= ":observability-conditions" [ObsConditionDef] ;
EmptyActionObs.         ActionObsDef            ::= ;

-- Observability Conditions 
EmptyObsCond.           ObsConditionDef         ::= "("             ObservingAgent ObservingAgentGroup              ")" ;
ObsCond.                ObsConditionDef         ::= "("             ObservingAgent ObservingAgentGroup "if" Formula ")" ;
ObsOtherwiseCond.       ObsConditionDef         ::= "(" "otherwise" ObservingAgent ObservingAgentGroup              ")" ;
separator nonempty      ObsConditionDef "" ;



----- EPDDL Action Type Library -----
EPDDLLibrary.           LibraryDef              ::= "(" "define" "(" "library" LibraryName ")" [LibraryItemDef] ")" ;

EPDDLLibRequire.        LibraryItemDef          ::= RequireDef ;
EPDDLLibModalities.     LibraryItemDef          ::= ModalitiesDef ;
EPDDLLibObsGroups.      LibraryItemDef          ::= ObservabilityGroupsDef ;
EPDDLLibActionType.     LibraryItemDef          ::= ActionTypeDef ;
EPDDLLibEvent.          LibraryItemDef          ::= EventDef ;
separator               LibraryItemDef "" ;


--- EPDDL Action Types Definition
EPDDLActType.           ActionTypeDef           ::= "(" ":action-type" ActionTypeName ActionTypeParameterDef ActionTypeFrameDef ActionTypeEventsDef ActionTypeRelDef ActionTypeDesDef ")" ;

ActTypeParam.           ActionTypeParameterDef  ::= ":parameters" "(" TypedVariableList ")" ;

ActTypeFrame.           ActionTypeFrameDef      ::= ":frame-of-reference" "(" [ObservingAgentGroup] ")" ;
EmptyActTypeFrame.      ActionTypeFrameDef      ::= ;

ActTypeEvents.          ActionTypeEventsDef     ::= ":events" [EventSignature] ;
ActTypeRel.             ActionTypeRelDef        ::= ":relations" ActionRelations ;
ActTypeDes.             ActionTypeDesDef        ::= ":designated" "(" [EventName] ")" ;

EventSign.              EventSignature          ::= "(" EventName [Parameter] ")" ;
separator nonempty      EventSignature "" ;

EventsActionRel.        ActionRelations         ::= [EventRelation] ;
TrivialActionRel.       ActionRelations         ::= TrivialDef ;

EventRel.               EventRelation           ::= "(" AgentGroup [EventNamePair] ")" ;
TrivialEventRel.        EventRelation           ::= "(" AgentGroup TrivialDef ")" ;
separator nonempty      EventRelation "" ;

EventPair.              EventNamePair           ::= "(" EventName EventName ")" ;
separator nonempty      EventNamePair "" ;


--- EPDDL Events Definition
EPDDLEvent.             EventDef                ::= "(" ":event" EventName EventParameterDef EventPreDef EventPostDef ")" ;

EventParam.             EventParameterDef       ::= ":parameters" "(" TypedVariableList ")"  ;

EventPre.               EventPreDef             ::= ":precondition" FormulaOrEmpty ;

EventPost.              EventPostDef            ::= ":postconditions" EventPostconditions ;
EmptyEventPost.         EventPostDef            ::= ;

Postconditions.         EventPostconditions     ::= [Postcondition] ;
TrivialPostconditions.  EventPostconditions     ::= TrivialDef ;

ForallPostcondition.    Postcondition           ::= "(" "forall" "(" TypedVariableList ")" [LiteralPostcondition] ")" ;
SinglePostcondition.    Postcondition           ::= LiteralPostcondition ;
VarPostcondition.       Postcondition           ::= "(" Variable ")" ;
separator nonempty      Postcondition "" ;

LiteralPost.            LiteralPostcondition    ::= "(" "set" Literal "iff" FormulaOrEmpty ")" ;
TrivialLiteralPost.     LiteralPostcondition    ::= "(" "set" Literal ")" ;
separator nonempty      LiteralPostcondition "" ;



----- EPDDL Problem -----
EPDDLProblem.           ProblemDef              ::= "(" "define" "(" "problem" ProblemName ")" [ProblemItemDef] ")" ;

EPDDLProbDomain.        ProblemItemDef          ::= ProblemDomainNameDef ;
EPDDLProbRequire.       ProblemItemDef          ::= RequireDef ;
EPDDLProbModalities.    ProblemItemDef          ::= ModalitiesDef ;
EPDDLProbAgents.        ProblemItemDef          ::= AgentNamesDef ;
EPDDLProbAgentGroups.   ProblemItemDef          ::= AgentGroupsListDef ;
EPDDLProbObjects.       ProblemItemDef          ::= ObjectNamesDef ;
EPDDLProbFacts.         ProblemItemDef          ::= FactListDef ;
EPDDLProbInit.          ProblemItemDef          ::= InitDef ;
EPDDLProbInitModel.     ProblemItemDef          ::= InitialModelDef ;
EPDDLProbGoal.          ProblemItemDef          ::= GoalDef ;
separator               ProblemItemDef "" ;


--- EPDDL Problem Domain Definition
EPDDLProbDomainName.    ProblemDomainNameDef    ::= "(" ":domain" DomainName ")" ;


--- EPDDL Agents Definition
EPDDLAgentNames.        AgentNamesDef           ::= "(" ":agents" [AgentName] ")" ;


--- EPDDL Agent Groups Definition
EPDDLAgentGroupsList.   AgentGroupsListDef      ::= "(" ":agent-groups" [AgentGroupDef] ")" ;

EPDDLAgentGroupDef.     AgentGroupDef           ::= "(" "{" [AgentName] "}" "as" AgentGroupName ")" ;
separator               AgentGroupDef "" ;


--- EPDDL Objects Definition
EPDDLObjectNames.       ObjectNamesDef          ::= "(" ":objects" TypedIdentList ")" ;


--- EPDDL Facts Definition
EPDDLFactList.          FactListDef             ::= "(" ":facts" [FactDef] ")" ;
EPDDLFactDef.           FactDef                 ::= "(" PredicateName [GenericName] ")" ;
separator nonempty      FactDef "" ;


--- EPDDL Initial State Definition
EPDDLInitialState.      InitDef                 ::= "(" ":init" InitialStateDescr ")" ;

FinitaryTheoryDescr.    InitialStateDescr       ::= [FTheoryFormula] ;
InitialModelDescr.      InitialStateDescr       ::= "(" ":model-name" ModelName ")" ;

-- Finitary Theory
FTheoryPredForm.        FTheoryFormula          ::= PredicateFormula ;
FTheoryCKPredForm.      FTheoryFormula          ::= "[" AllAgents "]" PredicateFormula ;
FTheoryCKKPredForm.     FTheoryFormula          ::= "[" AllAgents "]" KPredicateFormula ;
FTheoryCKOrKPredForm.   FTheoryFormula          ::= "[" AllAgents "]" KWPredicateFormula ;
FTheoryCKAndKPredForm.  FTheoryFormula          ::= "[" AllAgents "]" NotKWPredicateFormula ;
_.                      FTheoryFormula          ::= "(" FTheoryFormula ")" ;
separator nonempty      FTheoryFormula "" ;

KPredFormula.           KPredicateFormula       ::= "[" AgentName "]" PredicateFormula ;
_.                      KPredicateFormula       ::= "(" KPredicateFormula ")" ;

KWPredFormula.          KWPredicateFormula      ::= KnowsWhether PredicateFormula ;
_.                      KWPredicateFormula      ::= "(" KWPredicateFormula ")" ;

NotKWPredFormula.       NotKWPredicateFormula   ::= "(" "not" KnowsWhether PredicateFormula ")" ;
_.                      NotKWPredicateFormula   ::= "(" NotKWPredicateFormula ")" ;


--- EPDDL Initial Model
EPDDLInitialModel.      InitialModelDef         ::= "(" ":model" ModelName ModelWorldsDef ModelRelDef ModelValDef ModelDesDef ")" ;

ModelWorlds.            ModelWorldsDef          ::= ":worlds" "(" [WorldName] ")" ;
ModelRel.               ModelRelDef             ::= ":relations" ModelRelations ;
ModelVal.               ModelValDef             ::= ":valuation" ModelValuation ;
ModelDes.               ModelDesDef             ::= ":designated" "(" [WorldName] ")" ;

WorldsModelRel.         ModelRelations          ::= [WorldRelation] ;
TrivialModelRel.        ModelRelations          ::= TrivialDef ;

WorldsModelVal.         ModelValuation          ::= [WorldValuation] ;
TrivialModelVal.        ModelValuation          ::= TrivialDef ;

WorldRel.               WorldRelation           ::= "(" AgentGroup [WorldNamePair] ")" ;
TrivialWorldRel.        WorldRelation           ::= "(" AgentGroup TrivialDef ")" ;
separator nonempty      WorldRelation "" ;

WorldPair.              WorldNamePair           ::= "(" WorldName WorldName ")" ;
separator nonempty      WorldNamePair "" ;

WorldVal.               WorldValuation          ::= "(" WorldName "[" [Literal] "]" ")" ;
separator nonempty      WorldValuation "" ;


--- EPDDL Goal Definition
EPDDLGoals.             GoalDef                 ::= "(" ":goal" Formula ")" ;



----- EPDDL Formulae -----
ImplyFormula.           Formula                 ::= "(" "imply" Formula  Formula  ")" ;
OrFormula.              Formula                 ::= "(" "or"    Formula [Formula] ")" ;
AndFormula.             Formula                 ::= "(" "and"   Formula [Formula] ")" ;
NotFormula.             Formula                 ::= "(" "not"   Formula           ")" ;

ModalFormula.           Formula                 ::= Modality Formula ;

VarFormula.             Formula                 ::= "(" Variable ")" ;
AtmFormula.             Formula                 ::= AtomicFormula ;
AtmEqFormula.           Formula                 ::= AtomicEqFormula ;
TrueFormula.            Formula                 ::= "true" ;
FalseFormula.           Formula                 ::= "false" ;
_.                      Formula                 ::= "(" Formula ")" ;
separator nonempty      Formula "" ;

Predicate.              AtomicFormula           ::= "(" PredicateName [MetaTerm] ")" ;
EqFormula.              AtomicEqFormula         ::= "(" "=" Term Term ")" ;

NonTrivialFormula.      FormulaOrEmpty          ::= Formula ;
TrivialFormula.         FormulaOrEmpty          ::= TrivialDef ;


--- EPDDL Terms
EPDDLMetaTerm.          MetaTerm                ::= Term ;
EPDDLMetaTermAnonVar.   MetaTerm                ::= AnonVarAgent ;
separator               MetaTerm "" ;

EPDDLGroundTerm.        Term                    ::= GroundTerm ;
EPDDLTermVar.           Term                    ::= Variable ;
separator               Term "" ;

EPDDLGroundTermName.    GroundTerm              ::= Name ;
EPDDLGroundTermAgName.  GroundTerm              ::= AgentName ;
separator               GroundTerm "" ;


--- EPDDL Modalities
EPDDLLabeledMod.        Modality                ::= SingleModality ;
EPDDLLabeledGroupMod.   Modality                ::= GroupModality ;

LabBoxMod.              SingleModality          ::= "[" ModalityLabel ModalityAgent "]" ;
LabDiamondMod.          SingleModality          ::= "<" ModalityLabel ModalityAgent ">" ;

LabBoxGroupMod.         GroupModality           ::= "[" ModalityLabel ModalityAgentGroup "]" ;
LabDiamondGroupMod.     GroupModality           ::= "<" ModalityLabel ModalityAgentGroup ">" ;

ModLabel.               ModalityLabel           ::= ModalityName ;
EmptyModLabel.          ModalityLabel           ::= ;

-- Used in Finitary Theories definition
KnowsWhetherMod.        KnowsWhether            ::= "[" ModalityName AgentName "]" ;    -- We force ModalityName to be "kw."


--- Literals
PosLiteral.             Literal                 ::= AtomicFormula ;
NegLiteral.             Literal                 ::= "(" "not" AtomicFormula ")" ;
separator               Literal "" ;


--- EPDDL Conditions
AndCondFomula.          ConditionFormula        ::= "(" "and" Condition [Condition] ")" ;
CondFomula.             ConditionFormula        ::= Condition ;

AtmCond.                Condition               ::= AtomicCondition ;
NotAtmCond.             Condition               ::= "(" "not" AtomicCondition ")" ;
separator nonempty      Condition "" ;

AtmCondPredicate.       AtomicCondition         ::= "(" PredicateName [Term] ")" ;
AtmCondEqFormula.       AtomicCondition         ::= "(" "=" Term Term ")" ;


--- Predicate Formulae
ImplyPredFormula.       PredicateFormula        ::= "(" "imply"  PredicateFormula  PredicateFormula  ")" ;
OrPredFormula.          PredicateFormula        ::= "(" "or"     PredicateFormula [PredicateFormula] ")" ;
AndPredFormula.         PredicateFormula        ::= "(" "and"    PredicateFormula [PredicateFormula] ")" ;
NotPredFormula.         PredicateFormula        ::= "(" "not"    PredicateFormula                    ")" ;

AtmPredFormula.         PredicateFormula        ::= "(" PredicateName [GroundTerm] ")" ;
separator nonempty      PredicateFormula "" ;



----- Basic EPDDL Elements -----

--- EPDDL Generic Name
EPDDLGenericNameObj.    GenericName             ::= Name ;
EPDDLGenericNameAg.     GenericName             ::= AgentName ;
separator               GenericName "" ;


--- Typed Name List
IdList.                 TypedIdentList          ::= [Name] ;
TypedIdList.            TypedIdentList          ::= Name [Name] "-" Type TypedIdentList ;


--- Typed Variable List
VarList.                TypedVariableList       ::= [Variable] ;
TypedVarList.           TypedVariableList       ::= Variable [Variable] "-" Type TypedVariableList ;


--- EPDDL Predicate Name
EPDDLPredicate.         PredicateName           ::= Name ;


-- EPDDL Agents
EPDDLModAgent.          ModalityAgent           ::= AgentName ;         -- Agents and agent groups
EPDDLModVarAgent.       ModalityAgent           ::= Variable ;
EPDDLModAnonVarAgent.   ModalityAgent           ::= AnonVarAgent ;
EPDDLModAllAgents.      ModalityAgent           ::= AllAgents ;
separator nonempty      ModalityAgent "" ;

EPDDLModAgList.         ModalityAgentGroup      ::= ModalityAgent [ModalityAgent] ;

EPDDLSingleAgentGroup.  AgentGroupName          ::= AgentName ;
EPDDLAllAgentsGroup.    AgentGroupName          ::= AllAgents ;

EPDDLObsAgentGroup.     ObservingAgentGroup     ::= AgentName ;
separator nonempty      ObservingAgentGroup "" ;

EPDDLObsAgent.          ObservingAgent          ::= AgentName ;
EPDDLObsAllAgents.      ObservingAgent          ::= AllAgents ;
EPDDLObsVarAgent.       ObservingAgent          ::= Variable ;
EPDDLObsAnonVarAgent.   ObservingAgent          ::= AnonVarAgent ;

EPDDLAgentGroup.        AgentGroup              ::= AgentGroupName ;
EPDDLAgentNameGroup.    AgentGroup              ::= "{" AgentName [AgentName] "}" ;

EPDDLAllAgents.         AllAgents               ::= "All" ;
EPDDLAnonVarAgent.      AnonVarAgent            ::= "?_" ;


--- EPDDL Parameters
EPDDLParam.             Parameter               ::= "(" Variable "::" ParameterValue ")" ;
separator               Parameter "" ;

EPDDLTermParam.         ParameterValue          ::= Term ;
EPDDLFormulaParam.      ParameterValue          ::= Formula ;
EPDDLPostParam.         ParameterValue          ::= [PostParameterValue] ;

PostParamForall.        PostParameterValue      ::= "(" "forall" "(" TypedVariableList ")" [LiteralPostcondition] ")" ;
PostParamLiteral.       PostParameterValue      ::= LiteralPostcondition ;
separator nonempty      PostParameterValue "" ;


--- EPDDL Types
EPDDLTypeName.          Type                    ::= Name ;
EPDDLResType.           Type                    ::= ReservedType ;

ResAgentType.           ReservedType            ::= "agent" ;
PredicateType.          ReservedType            ::= "predicate" ;
LiteralType.            ReservedType            ::= "literal" ;
PredFormulaType.        ReservedType            ::= "predicate-formula" ;
FormulaType.            ReservedType            ::= "formula" ;
PostconditionType.      ReservedType            ::= "postcondition" ;


--- EPDDL Domain Name
EPDDLDomainName.        DomainName              ::= Name ;


--- EPDDL Library Name

EPDDLLibraryName.       LibraryName             ::= Name ;
separator nonempty      LibraryName "" ;


--- EPDDL Problem Name
EPDDLProblemName.       ProblemName             ::= Name ;


--- EPDDL Action Name
EPDDLActionName.        ActionName              ::= Name ;


--- EPDDL Action Type Name
EPDDLActTypeName.       ActionTypeName          ::= Name ;


--- EPDDL Event Name
EPDDLEventName.         EventName               ::= Name ;
separator nonempty      EventName "" ;


--- EPDDL Models
EPDDLModelName.         ModelName               ::= Name ;
EPDDLWorldName.         WorldName               ::= Name ;
separator nonempty      WorldName "" ;


--- EPDDL Requirements
EPDDLReqDel.            RequireKey              ::= ":del" ;
EPDDLReqTyping.         RequireKey              ::= ":typing" ;
EPDDLReqEquality.       RequireKey              ::= ":equality" ;
EPDDLReqParamList.      RequireKey              ::= ":parameter-lists" ;

EPDDLReqNegPre.         RequireKey              ::= ":negative-preconditions" ;
EPDDLReqDisPre.         RequireKey              ::= ":disjunctive-preconditions" ;
EPDDLReqExiPre.         RequireKey              ::= ":existential-preconditions" ;
EPDDLReqUniPre.         RequireKey              ::= ":universal-preconditions" ;

EPDDLReqModPre.         RequireKey              ::= ":modal-preconditions" ;
EPDDLReqModPost.        RequireKey              ::= ":modal-postconditions" ;
EPDDLReqModalities.     RequireKey              ::= ":modalities" ;

EPDDLReqOnticChange.    RequireKey              ::= ":ontic-change" ;

EPDDLReqCK.             RequireKey              ::= ":common-knowledge" ;
EPDDLReqDynCK.          RequireKey              ::= ":dynamic-common-knowledge" ;

EPDDLReqMAStar.         RequireKey              ::= ":ma-star" ;        -- Group requirement: :parameter-lists, :modalities, :ma-star-ontic, :ma-star-sensing, :ma-star-announcement, :ma-star-finitary-theory
EPDDLReqOntic.          RequireKey              ::= ":ma-star-ontic" ;
EPDDLReqSensing.        RequireKey              ::= ":ma-star-sensing" ;
EPDDLReqAnnouncement.   RequireKey              ::= ":ma-star-announcement" ;
EPDDLReqFTheory.        RequireKey              ::= ":ma-star-finitary-theory" ;

EPDDLReqMaxPreDepth.    RequireKey              ::= "(" ":maximum-preconditions-depth" Integer ")" ;
EPDDLReqMaxPostDepth.   RequireKey              ::= "(" ":maximum-postconditions-depth" Integer ")" ;
EPDDLReqMaxDepth.       RequireKey              ::= "(" ":maximum-modal-depth" Integer ")" ;
separator nonempty      RequireKey "" ;
-- todo: trova paper con teorema su epistemic actions con propositional preconditions


--- Trivial Definition
EPDDLTrivialDef.        TrivialDef              ::= "(" ")" ;



----- Tokens -----

--- Names
position token          Name                        (lower (letter|digit|'_'|'\'')*) ;
separator               Name "" ;

position token          AgentName                   (upper (letter|digit|'_')*) ;
separator nonempty      AgentName "" ;

position token          ModalityName                (lower (letter|digit|'_'|'\'')* '.') ;
separator               ModalityName "" ;

--- Variables
position token          Variable                    ('?' (letter (letter|digit|'_'|'\'')*)) ;
separator               Variable "" ;
