---------- LBNF Grammar for EPDDL ----------
--
-- Authors: Alessandro Burigana, Francesco Fabiano
-- License: MIT
--


-- EPDDL Comments
comment                 ";" ;


-- Parsing Entrypoints
entrypoints             MainDef ;

-- Using MainDef because multiple entrypoints are not supported by BNFC cpp backend
-- Domain, action-type library and problem definitions MUST come in different files
Domain.                 MainDef                 ::= DomainDef ;
ActionTypeLibrary.      MainDef                 ::= LibraryDef ;
Problem.                MainDef                 ::= ProblemDef ;



----- EPDDL Domain -----
EPDDLDomain.            DomainDef               ::= "(" "define" "(" "domain" Name ")" [DomainItemDef] ")" ;

EPDDLDomLibrary.        DomainItemDef           ::= DomainLibrariesNameDef ;
EPDDLDomRequire.        DomainItemDef           ::= RequireDef ;
EPDDLDomTypes.          DomainItemDef           ::= TypesDef ;
EPDDLDomPredicates.     DomainItemDef           ::= PredicateListDef ;

EPDDLDomObsGroups.      DomainItemDef           ::= ObservabilityGroupsDef ;
EPDDLDomAction.         DomainItemDef           ::= ActionDef ;
separator               DomainItemDef "" ;


--- EPDDL Domain Library Definition
EPDDLDomLibraryNames.   DomainLibrariesNameDef  ::= "(" ":action-type-libraries" [LibraryName] ")" ;


--- EPDDL Requirements Definition
EPDDLRequire.           RequireDef              ::= "(" ":requirements" [RequireKey] ")" ;


--- EPDDL Types Definition
EPDDLTypes.             TypesDef                ::= "(" ":types" BasicTypedIdentList ")" ;


--- EPDDL Predicates Definition
EPDDLPredicateList.     PredicateListDef        ::= "(" ":predicates" [PredicateDef] ")" ;
EPDDLPredicateDef.      PredicateDef            ::= "(" PredicateName BasicTypedVariableList ")" ;
separator nonempty      PredicateDef "" ;


--- EPDDL Observability Groups Definition
EPDDLObsGroupsNames.    ObservabilityGroupsDef  ::= "(" ":observability-groups" [ObservingAgentGroup] ")" ;


--- EPDDL Actions Definition
EPDDLAction.            ActionDef               ::= "(" ":action" ActionName ActionParameterDef ActionTypeSignatureDef ActionPreDef ActionObsDef ")" ;

ActionPar.              ActionParameterDef      ::= ":parameters" "(" BasicTypedVariableList ")" ;

ActionSignature.        ActionTypeSignatureDef  ::= ":action-type" "(" ActionTypeName [Parameter] ")" ;

ActionPre.              ActionPreDef            ::= ":precondition" Precondition ;

FormulaPrecondition.    Precondition            ::= Formula ;
TrivialPrecondition.    Precondition            ::= TrivialDef ;

ActionObs.              ActionObsDef            ::= ":observability-conditions" [ObsConditionDef] ;
EmptyActionObs.         ActionObsDef            ::= ;

-- Observability Conditions 
EmptyObsCond.           ObsConditionDef         ::= "("             ObservingAgent ObservingAgentGroup                ")" ;
ObsCond.                ObsConditionDef         ::= "("             ObservingAgent ObservingAgentGroup "if" Formula ")" ;
ObsOtherwiseCond.       ObsConditionDef         ::= "(" "otherwise" ObservingAgent ObservingAgentGroup                ")" ;
separator nonempty      ObsConditionDef "" ;



----- EPDDL Action Type Library -----
EPDDLLibrary.           LibraryDef              ::= "(" "define" "(" "library" Name ")" [LibraryItemDef] ")" ;

EPDDLLibRequire.        LibraryItemDef          ::= RequireDef ;
EPDDLLibObsGroups.      LibraryItemDef          ::= ObservabilityGroupsDef ;

EPDDLLibActionType.     LibraryItemDef          ::= ActionTypeDef ;
EPDDLLibEvent.          LibraryItemDef          ::= EventDef ;
separator               LibraryItemDef "" ;


--- EPDDL Action Types Definition
EPDDLActType.           ActionTypeDef           ::= "(" ":action-type" ActionTypeName ActionTypeParameterDef ActionTypeFrameDef ActionTypeEventsDef ActionTypeRelDef ActionTypeDesDef ")" ;

ActTypePar.             ActionTypeParameterDef  ::= ":parameters" "(" TypedVariableList ")" ;

ActTypeFrame.           ActionTypeFrameDef      ::= ":frame-of-reference" "(" [ObservingAgentGroup] ")" ;
EmptyActTypeFrame.      ActionTypeFrameDef      ::= ;

ActTypeEvents.          ActionTypeEventsDef     ::= ":events" [EventSignature] ;
ActTypeRel.             ActionTypeRelDef        ::= ":relations" ActionRelations ;
ActTypeDes.             ActionTypeDesDef        ::= ":designated" "(" [EventName] ")" ;

EventSign.              EventSignature          ::= "(" EventName [Parameter] ")" ;
separator nonempty      EventSignature "" ;

EventsActionRel.        ActionRelations         ::= [EventRelation] ;
TrivialActionRel.       ActionRelations         ::= TrivialDef ;

EventRel.               EventRelation           ::= "(" AgentList [EventNamePair] ")" ;
TrivialEventRel.        EventRelation           ::= "(" AgentList TrivialDef ")" ;
separator nonempty      EventRelation "" ;

EventPair.              EventNamePair           ::= "(" EventName EventName ")" ;
separator nonempty      EventNamePair "" ;


--- EPDDL Events Definition
EPDDLEvent.             EventDef                ::= "(" ":event" EventName EventParameterDef EventPreDef EventPostDef ")" ;

EventPar.               EventParameterDef       ::= ":parameters" "(" TypedVariableList ")"  ;

EventPre.               EventPreDef             ::= ":precondition" Precondition ;

EventPost.              EventPostDef            ::= ":postconditions" EventPostconditions ;
EmptyEventPost.         EventPostDef            ::= ;

Postconditions.         EventPostconditions     ::= [LiteralPostcondition] ;
TrivialPostconditions.  EventPostconditions     ::= TrivialDef ;

LiteralPost.            LiteralPostcondition    ::= "(" Literal "if" Postcondition ")" ;
TrivialLiteralPost.     LiteralPostcondition    ::= "(" Literal ")" ;
separator nonempty      LiteralPostcondition "" ;

FormulaPostcondition.   Postcondition           ::= Formula ;
TrivialPostcondition.   Postcondition           ::= TrivialDef ;



----- EPDDL Problem -----
EPDDLProblem.           ProblemDef              ::= "(" "define" "(" "problem" Name ")" [ProblemItemDef] ")" ;

EPDDLProbDomain.        ProblemItemDef          ::= ProblemDomainNameDef ;
EPDDLProbRequire.       ProblemItemDef          ::= RequireDef ;
EPDDLProbAgents.        ProblemItemDef          ::= AgentNamesDef ;
EPDDLProbAgentGroups.   ProblemItemDef          ::= AgentGroupsListDef ;
EPDDLProbObjects.       ProblemItemDef          ::= ObjectNamesDef ;
EPDDLProbInit.          ProblemItemDef          ::= InitDef ;
EPDDLProbInitModel.     ProblemItemDef          ::= InitialModelDef ;
EPDDLProbGoal.          ProblemItemDef          ::= GoalDef ;
separator               ProblemItemDef "" ;


--- EPDDL Problem Domain Definition
EPDDLDomainName.        ProblemDomainNameDef    ::= "(" ":domain" Name ")" ;


--- EPDDL Agents Definition
EPDDLAgentNames.        AgentNamesDef           ::= "(" ":agents" [AgentName] ")" ;

--- EPDDL Agent Groups Definition
EPDDLAgentGroupsList.   AgentGroupsListDef      ::= "(" ":agent-groups" [AgentGroupDef] ")" ;

EPDDLAgentGroupDef.     AgentGroupDef           ::= "(" "[" [AgentName] "]" "as" AgentGroupName ")" ;
separator               AgentGroupDef "" ;


--- EPDDL Objects Definition
EPDDLObjectNames.       ObjectNamesDef          ::= "(" ":objects" BasicTypedIdentList ")" ;


--- EPDDL Initial State Definition
EPDDLInitialState.      InitDef                 ::= "(" ":init" InitialStateDescr ")" ;

FinitaryTheoryDescr.    InitialStateDescr       ::= [FTheoryFormula] ;
InitialModelDescr.      InitialStateDescr       ::= "(" ":model-name" ModelName ")" ;

-- Finitary Theory
FTheoryPredForm.        FTheoryFormula          ::= PredicateFormula ;
FTheoryCKPredForm.      FTheoryFormula          ::= "[" AllAgents "]" PredicateFormula ;
FTheoryCKKPredForm.     FTheoryFormula          ::= "[" AllAgents "]" KPredicateFormula ;
FTheoryCKOrKPredForm.   FTheoryFormula          ::= "[" AllAgents "]" KWPredicateFormula ;
FTheoryCKAndKPredForm.  FTheoryFormula          ::= "[" AllAgents "]" NotKWPredicateFormula ;
_.                      FTheoryFormula          ::= "(" FTheoryFormula ")" ;
separator nonempty      FTheoryFormula "" ;

KPredFormula.           KPredicateFormula       ::= "[" AgentName "]" PredicateFormula ;
_.                      KPredicateFormula       ::= "(" KPredicateFormula ")" ;

KWPredFormula.          KWPredicateFormula      ::= KnowsWhether PredicateFormula ;
_.                      KWPredicateFormula      ::= "(" KWPredicateFormula ")" ;

NotKWPredFormula.       NotKWPredicateFormula   ::= "(" "not" KnowsWhether PredicateFormula ")" ;
_.                      NotKWPredicateFormula   ::= "(" NotKWPredicateFormula ")" ;


--- EPDDL Initial Model
EPDDLInitialModel.      InitialModelDef         ::= "(" ":model" ModelName ModelWorldsDef ModelRelDef ModelValDef ModelDesDef ")" ;

ModelWorlds.            ModelWorldsDef          ::= ":worlds" "(" [WorldName] ")" ;
ModelRel.               ModelRelDef             ::= ":relations" ModelRelations ;
ModelVal.               ModelValDef             ::= ":valuation" ModelValuation ;
ModelDes.               ModelDesDef             ::= ":designated" "(" [WorldName] ")" ;

WorldsModelRel.         ModelRelations          ::= [WorldRelation] ;
TrivialModelRel.        ModelRelations          ::= TrivialDef ;

WorldsModelVal.         ModelValuation          ::= [WorldValuation] ;
TrivialModelVal.        ModelValuation          ::= TrivialDef ;

WorldRel.               WorldRelation           ::= "(" AgentList [WorldNamePair] ")" ;
TrivialWorldRel.        WorldRelation           ::= "(" AgentList TrivialDef ")" ;
separator nonempty      WorldRelation "" ;

WorldPair.              WorldNamePair           ::= "(" WorldName WorldName ")" ;
separator nonempty      WorldNamePair "" ;

WorldVal.               WorldValuation          ::= "(" WorldName "[" [Literal] "]" ")" ;
separator nonempty      WorldValuation "" ;


--- EPDDL Goal Definition
EPDDLGoals.             GoalDef                 ::= "(" ":goal" Formula ")" ;



----- EPDDL Formulae -----
ImplyFormula.           Formula                 ::= "(" "imply"          Formula  Formula  ")" ;
OrFormula.              Formula                 ::= "(" "or"             Formula [Formula] ")" ;
AndFormula.             Formula                 ::= "(" "and"            Formula [Formula] ")" ;
NotFormula.             Formula                 ::= "(" "not"            Formula           ")" ;

ModalFormula.           Formula                 ::= Modality Formula ;

AtmFormula.             Formula                 ::= AtomicFormula ;
AtmEqFormula.           Formula                 ::= AtomicEqFormula ;
TrueFormula.            Formula                 ::= "true" ;
FalseFormula.           Formula                 ::= "false" ;
_.                      Formula                 ::= "(" Formula ")" ;
separator nonempty      Formula "" ;

Predicate.              AtomicFormula           ::= "(" PredicateName [MetaTerm] ")" ;
VarFormula.             AtomicFormula           ::= "(" Variable ")" ;
EqFormula.              AtomicEqFormula         ::= "(" "=" Term Term ")" ;


--- EPDDL Terms
EPDDLMetaTerm.          MetaTerm                ::= Term ;
EPDDLMetaTermAnonVar.   MetaTerm                ::= AnonVarAgent ;
separator               MetaTerm "" ;  

EPDDLTermName.          Term                    ::= Name ;
EPDDLTermAgentName.     Term                    ::= AgentName ;
EPDDLTermVar.           Term                    ::= Variable ;


--- EPDDL Modalities
EPDDLLabeledMod.        Modality                ::= SingleModality ;
EPDDLLabeledGroupMod.   Modality                ::= GroupModality ;

LabBoxMod.              SingleModality          ::= "[" ModalityLabel ModalityAgent "]" ;
LabDiamondMod.          SingleModality          ::= "<" ModalityLabel ModalityAgent ">" ;

LabBoxGroupMod.         GroupModality           ::= "[" ModalityLabel ModalityAgentGroup "]" ;
LabDiamondGroupMod.     GroupModality           ::= "<" ModalityLabel ModalityAgentGroup ">" ;

ModLabel.               ModalityLabel           ::= ModalityName ;
KnowsWhetherLabel.      ModalityLabel           ::= "kw." ;         -- Knows Whether
EverybodyKnowsLabel.    ModalityLabel           ::= "e."  ;         -- Everybody Knows
SomeoneKnowsLabel.      ModalityLabel           ::= "s."  ;         -- Someone Knows
EmptyModLabel.          ModalityLabel           ::= ;

-- Used in Finitary Theories definition
KnowsWhetherMod.        KnowsWhether            ::= "[" "kw." AgentName "]" ;


--- Literals
PosLiteral.             Literal                 ::= AtomicFormula ;
NegLiteral.             Literal                 ::= "(" "not" AtomicFormula ")" ;
separator               Literal "" ;


--- Predicate Formulae
ImplyPredFormula.       PredicateFormula        ::= "(" "imply"  PredicateFormula  PredicateFormula  ")" ;
OrPredFormula.          PredicateFormula        ::= "(" "or"     PredicateFormula [PredicateFormula] ")" ;
AndPredFormula.         PredicateFormula        ::= "(" "and"    PredicateFormula [PredicateFormula] ")" ;
NotPredFormula.         PredicateFormula        ::= "(" "not"    PredicateFormula                    ")" ;

LitPredFormula.         PredicateFormula        ::= "(" PredicateName [Name] ")" ;
separator nonempty      PredicateFormula "" ;



----- Basic EPDDL Elements -----

-- Typed Name List
IdList.                 BasicTypedIdentList     ::= [Name] ;
TypedIdList.            BasicTypedIdentList     ::= Name [Name] "-" BasicType BasicTypedIdentList ;


-- Typed Variable List
BasicVarList.           BasicTypedVariableList  ::= [Variable] ;
BasicTypedVarList.      BasicTypedVariableList  ::= Variable [Variable] "-" BasicType BasicTypedVariableList ;

VarList.                TypedVariableList       ::= [Variable] ;
TypedVarList.           TypedVariableList       ::= Variable [Variable] "-" Type TypedVariableList ;


--- EPDDL Predicates
EPDDLPredicate.         PredicateName           ::= Name ;


-- EPDDL Agents
EPDDLModAgent.          ModalityAgent           ::= AgentName ;         -- Agents and agent groups
EPDDLModVarAgent.       ModalityAgent           ::= Variable ;
EPDDLModAnonVarAgent.   ModalityAgent           ::= AnonVarAgent ;
EPDDLModAllAgents.      ModalityAgent           ::= AllAgents ;
separator nonempty      ModalityAgent "" ;

EPDDLModAgList.         ModalityAgentGroup      ::= ModalityAgent [ModalityAgent] ;

EPDDLAgentGroup.        AgentGroupName          ::= AgentName ;
EPDDLAllAgentsGroup.    AgentGroupName          ::= AllAgents ;

EPDDLObsAgentGroup.     ObservingAgentGroup     ::= AgentName ;
separator nonempty      ObservingAgentGroup "" ;

EPDDLObsAgent.          ObservingAgent          ::= AgentName ;
EPDDLObsVarAgent.       ObservingAgent          ::= Variable ;
EPDDLObsAnonVarAgent.   ObservingAgent          ::= AnonVarAgent ;

EPDDLSingleAgentList.   AgentList               ::= AgentName ;
EPDDLAgentNameList.     AgentList               ::= "[" AgentName [AgentName] "]" ;
EPDDLAllAgentsList.     AgentList               ::= AllAgents ;

EPDDLAllAgents.         AllAgents               ::= "All" ;
EPDDLAnonVarAgent.      AnonVarAgent            ::= "?_" ;


--- EPDDL Parameters
EPDDLBasicParam.        Parameter               ::= BasicParameter ;
EPDDLListParam.         Parameter               ::= "[" [Formula] "]" ;
separator               Parameter "" ;

EPDDLTermParam.         BasicParameter          ::= Term ;
EPDDLFormulaParam.      BasicParameter          ::= Formula ;
EPDDLTrivialParam.      BasicParameter          ::= TrivialDef ;


--- EPDDL Types
EPDDLBasicType.         Type                    ::= BasicType ;
EPDDLCompoundType.      Type                    ::= CompoundType ;

EPDDLBasicTypeName.     BasicType               ::= Name ;
EPDDLBasicResType.      BasicType               ::= ReservedBasicType ;

EPDDLFormulaListType.   CompoundType            ::= "[" ReservedFormulaType "]" ;

ResFormulaType.         ReservedBasicType       ::= ReservedFormulaType ;
ResAgentType.           ReservedBasicType       ::= "agent" ;

PredicateType.          ReservedFormulaType     ::= "predicate" ;
LiteralType.            ReservedFormulaType     ::= "literal" ;
PredFormulaType.        ReservedFormulaType     ::= "predicate-formula" ;
FormulaType.            ReservedFormulaType     ::= "formula" ;


--- EPDDL Library Name
EPDDLLibraryName.       LibraryName             ::= Name ;
separator nonempty      LibraryName "" ;


--- EPDDL Action Types
EPDDLResActTypeName.    ActionTypeName          ::= ReservedActionTypeName ;
EPDDLActTypeName.       ActionTypeName          ::= Name ;

OntActTypeName.         ReservedActionTypeName  ::= "ontic" ;
SenActTypeName.         ReservedActionTypeName  ::= "sensing" ;
AnnActTypeName.         ReservedActionTypeName  ::= "announcement" ;


--- EPDDL Events
EPDDLResEventName.      EventName               ::= ReservedEventName ;
EPDDLEventName.         EventName               ::= Name ;
separator nonempty      EventName "" ;

IdleEvent.              ReservedEventName       ::= "idle-event" ;


--- EPDDL Actions
EPDDLActionName.        ActionName              ::= Name ;


--- EPDDL Models
EPDDLModelName.         ModelName               ::= Name ;
EPDDLWorldName.         WorldName               ::= Name ;
separator nonempty      WorldName "" ;


--- EPDDL Requirements
EPDDLReqDel.            RequireKey              ::= ":del" ;
EPDDLReqTyping.         RequireKey              ::= ":typing" ;
EPDDLReqEquality.       RequireKey              ::= ":equality" ;
EPDDLReqParamList.      RequireKey              ::= ":parameter-lists" ;

EPDDLReqNegPre.         RequireKey              ::= ":negative-preconditions" ;
EPDDLReqDisPre.         RequireKey              ::= ":disjunctive-preconditions" ;
EPDDLReqExiPre.         RequireKey              ::= ":existential-preconditions" ;
EPDDLReqUniPre.         RequireKey              ::= ":universal-preconditions" ;

EPDDLReqModPre.         RequireKey              ::= ":modal-preconditions" ;
EPDDLReqModPost.        RequireKey              ::= ":modal-postconditions" ;

EPDDLReqOnticChange.    RequireKey              ::= ":ontic-change" ;

EPDDLReqCK.             RequireKey              ::= ":common-knowledge" ;
EPDDLReqDynCK.          RequireKey              ::= ":dynamic-common-knowledge" ;

EPDDLReqMAStar.         RequireKey              ::= ":ma-star" ;        -- Group requirement: :parameter-lists, :ma-star-ontic, :ma-star-sensing, :ma-star-announcement, :ma-star-finitary-theory
EPDDLReqOntic.          RequireKey              ::= ":ma-star-ontic" ;
EPDDLReqSensing.        RequireKey              ::= ":ma-star-sensing" ;
EPDDLReqAnnouncement.   RequireKey              ::= ":ma-star-announcement" ;
EPDDLReqFTheory.        RequireKey              ::= ":ma-star-finitary-theory" ;

EPDDLReqMaxPreDepth.    RequireKey              ::= "(" ":maximum-preconditions-depth" Integer ")" ;
EPDDLReqMaxPostDepth.   RequireKey              ::= "(" ":maximum-postconditions-depth" Integer ")" ;
EPDDLReqMaxDepth.       RequireKey              ::= "(" ":maximum-modal-depth" Integer ")" ;
separator nonempty      RequireKey "" ;
-- todo: trova paper con teorema su epistemic actions con propositional preconditions


--- Trivial Definition
EPDDLTrivialDef.        TrivialDef              ::= "(" ")" ;



----- Tokens -----

--- Names
position token          Name                        (lower (letter|digit|'_'|'\'')*) ;
separator               Name "" ;

position token          AgentName                   (upper (letter|digit|'_')*) ;
separator nonempty      AgentName "" ;

position token          ModalityName                (lower (letter|digit|'_'|'\'')* '.') ;

position token          Variable                    ('?' (letter (letter|digit|'_'|'\'')*)) ;
separator               Variable "" ;
