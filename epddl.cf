---------- LBNF Grammar for EPDDL ----------
--
-- Authors: Alessandro Burigana, Francesco Fabiano
-- License: MIT
--


-- EPDDL Comments
comment                 ";" ;


-- Parsing Entrypoints
entrypoints             MainDef ;

-- Using MainDef because multiple entrypoints are not supported by BNFC cpp backend
-- Domain and problem definitions MUST come in different files
Domain.                 MainDef                 ::= DomainDef ;
Problem.                MainDef                 ::= ProblemDef ;



----- EPDDL Domain -----
EPDDLDomain.            DomainDef               ::= "(" "define" "(" "domain" Name ")" [DomainItemDef] ")" ;

EPDDLDomRequire.        DomainItemDef           ::= RequireDef ;
EPDDLDomTypes.          DomainItemDef           ::= TypesDef ;
EPDDLDomPredicates.     DomainItemDef           ::= PredicateListDef ;
EPDDLDomAction.         DomainItemDef           ::= ActionDef ;
EPDDLDomActionType.     DomainItemDef           ::= ActionTypeDef ;
EPDDLDomEvent.          DomainItemDef           ::= EventDef ;
separator               DomainItemDef "" ;


--- EPDDL Requirements Definition
EPDDLRequire.           RequireDef              ::= "(" ":requirements" [RequireKey] ")" ;


--- EPDDL Types Definition
EPDDLTypes.             TypesDef                ::= "(" ":types" TypedIdentList ")" ;


--- EPDDL Predicates Definition
EPDDLPredicateList.     PredicateListDef        ::= "(" ":predicates" [PredicateDef] ")" ;
PredDef.                PredicateDef            ::= "(" PredicateName TypedVariableList ")" ;
separator nonempty      PredicateDef "" ;


--- EPDDL Actions Definition
EPDDLAction.            ActionDef               ::= "(" ":action" ActionName [ActionItemDef] ")" ;

ActionParItem.          ActionItemDef           ::= ActionParameterDef ;
ActionSignItem.         ActionItemDef           ::= ActionSignatureDef ;
ActionPreItem.          ActionItemDef           ::= ActionPreDef ;
ActionObsItem.          ActionItemDef           ::= ActionObsDef ;
separator nonempty      ActionItemDef "" ;

ActionPar.              ActionParameterDef      ::= ":parameters" "(" TypedVariableList ")" ;

ActionSignature.        ActionSignatureDef      ::= ":action-type" "(" ActionTypeName [Parameter] ")" ;

ActionPre.              ActionPreDef            ::= ":precondition" Precondition ;

FormulaPrecondition.    Precondition            ::= Formula ;
TrivialPrecondition.    Precondition            ::= "(" ")" ;

ActionObs.              ActionObsDef            ::= ":observability-conditions" [ObsConditionDef] ;

-- Observability Conditions 
EmptyObsCond.           ObsConditionDef         ::= "(" ObsAgent AgentGroupName ")" ;
ObsCond.                ObsConditionDef         ::= "(" ObsAgent AgentGroupName Formula ")" ;
ObsOtherwiseCond.       ObsConditionDef         ::= "(" "otherwise" ObsAgent AgentGroupName ")" ;
separator nonempty      ObsConditionDef "" ;

ObsAg.                  ObsAgent                ::= AgentName ;
ObsAgVar.               ObsAgent                ::= Variable ;
ObsAgAnon.              ObsAgent                ::= "?_" ;


--- EPDDL Action Types Definition
EPDDLActType.           ActionTypeDef           ::= "(" ":action-type" ActionTypeName [ActionTypeItemDef] ")" ;

ActTypeParItem.         ActionTypeItemDef       ::= ActionTypeParameterDef ;
ActTypeFrameItem.       ActionTypeItemDef       ::= ActionTypeFrameDef ;
ActTypeEventsItem.      ActionTypeItemDef       ::= ActionTypeEventsDef ;
ActTypeRelItem.         ActionTypeItemDef       ::= ActionTypeRelDef ;
ActTypeDesItem.         ActionTypeItemDef       ::= ActionTypeDesDef ;
separator nonempty      ActionTypeItemDef "" ;

ActTypePar.             ActionTypeParameterDef  ::= ":parameters" "(" TypedVariableList ")" ;

ActTypeFrame.           ActionTypeFrameDef      ::= ":frame-of-reference" "(" [AgentGroupName] ")" ;

ActTypeEvents.          ActionTypeEventsDef     ::= ":events" [EventSignature] ;
ActTypeRel.             ActionTypeRelDef        ::= ":relations" ActionRelations ;
ActTypeDes.             ActionTypeDesDef        ::= ":designated" "(" [EventName] ")" ;

EventSign.              EventSignature          ::= "(" EventName "(" [Parameter] ")" ")" ;
EventSignNoParam.       EventSignature          ::= "(" EventName ")" ;
separator nonempty      EventSignature "" ;

EventsActionRel.        ActionRelations         ::= [EventRelation] ;
TrivialActionRel.       ActionRelations         ::= "(" ")" ;

EventRel.               EventRelation           ::= "(" AgentOrGroupName [EventNamePair] ")" ;
separator nonempty      EventRelation "" ;

AgentOrGroup.           AgentOrGroupName        ::= AgentName ;
AgentOrGroupList.       AgentOrGroupName        ::= "(" [AgentName] ")" ;

EventPair.              EventNamePair           ::= "(" EventName EventName ")" ;
separator nonempty      EventNamePair "" ;


--- EPDDL Events Definition
EPDDLEvent.             EventDef                ::= "(" ":event" EventName [EventItemDef] ")" ;

EventParItem.           EventItemDef            ::= EventParameterDef ;
EventPreItem.           EventItemDef            ::= EventPreDef ;
EventPostItem.          EventItemDef            ::= EventPostDef ;
separator nonempty      EventItemDef "" ;

EventPar.               EventParameterDef       ::= ":parameters" "(" TypedVariableList ")"  ;
EventPre.               EventPreDef             ::= ":precondition" Precondition ;
EventPost.              EventPostDef            ::= ":postconditions" EventPostconditions ;

Postconditions.         EventPostconditions     ::= [LiteralPostcondition] ;
TrivialPostconditions.  EventPostconditions     ::= "(" ")" ;

LiteralPost.            LiteralPostcondition    ::= "(" Literal Formula ")" ;
TrivialLiteralPost.     LiteralPostcondition    ::= "(" Literal ")" ;
separator nonempty      LiteralPostcondition "" ;


--- EPDDL Formulae
ImplyFormula.           Formula                 ::= "(" "imply"          Formula  Formula  ")" ;
OrFormula.              Formula                 ::= "(" "or"             Formula [Formula] ")" ;
AndFormula.             Formula                 ::= "(" "and"            Formula [Formula] ")" ;
NotFormula.             Formula                 ::= "(" "not"            Formula           ")" ;

BoxFormula.             Formula                 ::= "(" "[" [Agent] "]" Formula ")" ;
DiaFormula.             Formula                 ::= "(" "<"  Agent  ">" Formula ")" ;

AtmFormula.             Formula                 ::= AtomicFormula ;
AtmEqFormula.           Formula                 ::= AtomicEqFormula ;
separator nonempty      Formula "" ;

Predicate.              AtomicFormula           ::= "(" PredicateName [MetaTerm] ")" ;
VarFormula.             AtomicFormula           ::= "(" Variable ")" ;
EqFormula.              AtomicEqFormula         ::= "(" "=" Term Term ")" ;

EPDDLMetaTerm.          MetaTerm                ::= Term ;
EPDDLMetaTermAnonVar.   MetaTerm                ::= "?_" ;
separator               MetaTerm "" ;  

EPDDLTermName.          Term                    ::= Name ;
EPDDLTermVar.           Term                    ::= Variable ;

PosLiteral.             Literal                 ::= AtomicFormula ;
NegLiteral.             Literal                 ::= "(" "not" AtomicFormula ")" ;
separator               Literal "" ;



----- EPDDL Problem -----
EPDDLProblem.           ProblemDef              ::= "(" "define" "(" "problem" Name ")" [ProblemItemDef] ")" ;

EPDDLProbDomain.        ProblemItemDef          ::= ProblemDomainNameDef ;
EPDDLProbRequire.       ProblemItemDef          ::= RequireDef ;
EPDDLProbAgents.        ProblemItemDef          ::= AgentNamesDef ;
EPDDLProbObjects.       ProblemItemDef          ::= ObjectNamesDef ;
EPDDLProbInit.          ProblemItemDef          ::= InitDef ;
EPDDLProbInitModel.     ProblemItemDef          ::= InitialModelDef ;
EPDDLProbGoal.          ProblemItemDef          ::= GoalDef ;
separator               ProblemItemDef "" ;


--- EPDDL Problem Domain Definition
EPDDLDomainName.        ProblemDomainNameDef    ::= "(" ":domain" Name ")" ;


--- EPDDL Agents Definition
EPDDLAgentNames.        AgentNamesDef           ::= "(" ":agents" [AgentName] ")" ;


--- EPDDL Objects Definition
EPDDLObjectNames.       ObjectNamesDef          ::= "(" ":objects" TypedIdentList ")" ;


--- EPDDL Initial State Definition
EPDDLInitialState.      InitDef                 ::= "(" ":init" InitialStateDescr ")" ;

FinitaryTheoryDescr.    InitialStateDescr       ::= [FTheoryFormula] ;
InitialModelDescr.      InitialStateDescr       ::= "(" ":model" ModelName ")" ;

-- Finitary Theory
FTheoryPredForm.        FTheoryFormula          ::= PredicateFormula ;
FTheoryCKPredForm.      FTheoryFormula          ::= "(" "[" "_" "]" PredicateFormula     ")" ;
FTheoryCKKPredForm.     FTheoryFormula          ::= "(" "[" "_" "]" KPredicateFormula    ")" ;
FTheoryCKOrKPredForm.   FTheoryFormula          ::= "(" "[" "_" "]" OrKPredicateFormula  ")" ;
FTheoryCKAndKPredForm.  FTheoryFormula          ::= "(" "[" "_" "]" AndKPredicateFormula ")" ;
separator nonempty      FTheoryFormula "" ;

KPredFormula.           KPredicateFormula       ::= "(" "["  Agent  "]" PredicateFormula ")" ;
OrKPredFormula.         OrKPredicateFormula     ::= "(" "or" KPredicateFormula KPredicateFormula ")" ;

AndKPredFormula.        AndKPredicateFormula    ::= "(" "and" NotKPredicateFormula NotKPredicateFormula ")" ;
NotKPredFormula.        NotKPredicateFormula    ::= "(" "not" KPredicateFormula ")" ;

ImplyPredFormula.       PredicateFormula        ::= "(" "imply"  PredicateFormula  PredicateFormula  ")" ;
OrPredFormula.          PredicateFormula        ::= "(" "or"     PredicateFormula [PredicateFormula] ")" ;
AndPredFormula.         PredicateFormula        ::= "(" "and"    PredicateFormula [PredicateFormula] ")" ;
NotPredFormula.         PredicateFormula        ::= "(" "not"    PredicateFormula                    ")" ;

LitPredFormula.         PredicateFormula        ::= AtomicFormula ;
separator nonempty      PredicateFormula "" ;


--- EPDDL Initial Model
EPDDLInitialModel.      InitialModelDef         ::= "(" ":model" [ModelItemDef] ")" ;

ModelWorldsItem.        ModelItemDef            ::= ModelWorldsDef ;
ModelRelItem.           ModelItemDef            ::= ModelRelDef ;
ModelValItem.           ModelItemDef            ::= ModelValDef ;
ModelDesItem.           ModelItemDef            ::= ModelDesDef ;
separator nonempty      ModelItemDef "" ;

ModelWorlds.            ModelWorldsDef          ::= ":worlds" [WorldName] ;
ModelRel.               ModelRelDef             ::= ":relations" ModelRelations ;
ModelVal.               ModelValDef             ::= ":valuation" ModelValuation ;
ModelDes.               ModelDesDef             ::= ":designated" [WorldName] ;

WorldsModelRel.         ModelRelations          ::= [WorldRelation] ;
TrivialModelRel.        ModelRelations          ::= "(" ")" ;

WorldsModelVal.         ModelValuation          ::= [WorldValuation] ;
TrivialModelVal.        ModelValuation          ::= "(" ")" ;

WorldRel.               WorldRelation           ::= "(" AgentOrGroupName [WorldNamePair] ")" ;
separator nonempty      WorldRelation "" ;

WorldPair.              WorldNamePair           ::= "(" WorldName WorldName ")" ;
separator nonempty      WorldNamePair "" ;

WorldVal.               WorldValuation          ::= "(" WorldName "(" [Literal] ")" ")" ;
separator nonempty      WorldValuation "" ;


--- EPDDL Goal Definition
EPDDLGoals.             GoalDef                 ::= "(" ":goal" [Formula] ")" ;


----- Basic EPDDL Elements -----

-- Typed Name List
IdList.                 TypedIdentList          ::= [Name] ;
TypedIdList.            TypedIdentList          ::= Name [Name] "-" Type TypedIdentList ;


-- Typed Variable List
VarList.                TypedVariableList       ::= [Variable] ;
TypedVarList.           TypedVariableList       ::= Variable [Variable] "-" Type TypedVariableList ;


--- EPDDL Predicates
EPDDLPredicate.         PredicateName           ::= Name ;


-- EPDDL Agents
EPDDLAgent.             Agent                   ::= AgentName ;
EPDDLAgentVar.          Agent                   ::= Variable ;
EPDDLAgentAnonVar.      Agent                   ::= "?_" ;
EPDDLAgentAny.          Agent                   ::= "_" ;
separator nonempty      Agent "" ;

EPDDLAgentGroupName.    AgentGroupName          ::= AgentName ;
separator nonempty      AgentGroupName "" ;


--- EPDDL Parameters
EPDDLActualParVar.      Parameter               ::= Variable ;
EPDDLActualParFromula.  Parameter               ::= Formula ;
EPDDLActualParAgent.    Parameter               ::= AgentName ;
separator               Parameter "" ;


--- EPDDL Types
EPDDLType.              Type                    ::= Name ;
EPDDLResType.           Type                    ::= ReservedType ;

ResFormulaType.         ReservedType            ::= ReservedFormulaType ;
ResAgentType.           ReservedType            ::= "agent" ;
ResObjectType.          ReservedType            ::= "object" ;

PredicateType.          ReservedFormulaType     ::= "predicate" ;
LiteralType.            ReservedFormulaType     ::= "literal" ;
PredFormulaType.        ReservedFormulaType     ::= "predicate-formula" ;
FormulaType.            ReservedFormulaType     ::= "formula" ;


--- EPDDL Events
EPDDLResEventName.      EventName               ::= ReservedEventName ;
EPDDLEventName.         EventName               ::= Name ;
separator nonempty      EventName "" ;

IdleEvent.              ReservedEventName       ::= "idle-event" ;


--- EPDDL Action Types
EPDDLResActTypeName.    ActionTypeName          ::= ReservedActionTypeName ;
EPDDLActTypeName.       ActionTypeName          ::= Name ;

OntActTypeName.         ReservedActionTypeName  ::= "ontic" ;
SenActTypeName.         ReservedActionTypeName  ::= "sensing" ;
AnnActTypeName.         ReservedActionTypeName  ::= "announcement" ;


--- EPDDL Actions
EPDDLActionName.        ActionName              ::= Name ;


--- EPDDL Models
EPDDLModelName.         ModelName               ::= Name ;
EPDDLWorldName.         WorldName               ::= Name ;
separator nonempty      WorldName "" ;


--- EPDDL Requirements
EPDDLReqDel.            RequireKey              ::= ":del" ;
EPDDLReqTyping.         RequireKey              ::= ":typing" ;
EPDDLReqNegPre.         RequireKey              ::= ":negative-preconditions" ;
EPDDLReqDisPre.         RequireKey              ::= ":disjunctive-preconditions" ;
EPDDLReqExiPre.         RequireKey              ::= ":existential-preconditions" ;
EPDDLReqUniPre.         RequireKey              ::= ":universal-preconditions" ;
EPDDLReqCondEff.        RequireKey              ::= ":conditional-effects" ;
EPDDLReqEquality.       RequireKey              ::= ":equality" ;
EPDDLReqModPre.         RequireKey              ::= ":modal-preconditions" ;
EPDDLReqModPost.        RequireKey              ::= ":modal-postconditions" ;
EPDDLReqOntic.          RequireKey              ::= ":ontic-change" ;
EPDDLReqCK.             RequireKey              ::= ":common-knowledge" ;
EPDDLReqDynCK.          RequireKey              ::= ":dynamic-common-knowledge" ;
EPDDLReqFTheory.        RequireKey              ::= ":finitary-theory" ;
EPDDLReqMaxDepth.       RequireKey              ::= "(" ":maximum-modal-depth" Integer ")" ;
separator nonempty      RequireKey "" ;
-- todo: trova paper con teorema su epistemic actions con propositional preconditions



----- Tokens -----

--- Names
position token          Name                        (lower (letter|digit|'_'|'\'')*) ;
separator               Name "" ;

position token          AgentName                   (upper (letter|digit|'_')*) ;
separator nonempty      AgentName "" ;

position token          Variable                    ('?' (letter (letter|digit|'_'|'\'')*)) ;
separator               Variable "" ;

-- position token          FormulaVariable             ({"f?"} (letter (letter|digit|'_'|'\'')*)) ;
-- separator               FormulaVariable "" ;

-- position token          FormalParameter             (('?'|{"f?"}) (letter (letter|digit|'_'|'\'')*)) ;
-- separator               FormalParameter "" ;

--- Parentheses
-- position token          TLeftParen                  '(' ;
-- position token          TRightParen                 ')' ;


--- Other Reserved Symbols
-- position token          TDash                       '-' ;
-- position token          TQuestionMark               '?' ;
-- position token          TAnon                       '_' ;


--- Main Domain Keywords
-- position token          TDefine                     {"define"} ;
-- position token          TDomain                     {"domain"} ;
-- position token          TProblem                    {"problem"} ;

-- position token          TColRequirements            {":requirements"} ;
-- position token          TColTypes                   {":types"} ;
-- position token          TColPredicates              {":predicates"} ;
-- position token          TColAction                  {":action"} ;
-- position token          TColParameters              {":parameters"} ;
-- position token          TColActionType              {":action-type"} ;

-- position token          TColDomain                  {":domain"} ;
-- position token          TColModel                   {":model"} ;

-- position token          TColPrecondition            {":precondition"} ;
-- position token          TColPostconditions          {":postconditions"} ;
-- position token          TColObsConditions           {":observability-conditions"} ;
-- position token          TColFrameOfReference        {":frame-of-reference"} ;

-- position token          TOtherwise                  {"otherwise"} ;
-- position token          TAnonAgentVar               {"?_"} ;


--- Main Problem Keywords
-- position token          TColAgents                  {":agents"} ;
-- position token          TColObjects                 {":objects"} ;

-- position token          TColInit                    {":init"} ;
-- position token          TColGoal                    {":goal"} ;


--- Types
-- position token          TAgentType                  {"agent"} ;
-- position token          TObjectType                 {"object"} ;
-- position token          TPredicateType              {"predicate"} ;
-- position token          TLiteralType                {"literal"} ;
-- position token          TPredFormulaType            {"predicate-formula"} ;
-- position token          TFormulaType                {"formula"} ;


--- Requirements
-- position token          TColDel                     {":del"} ;
-- position token          TColTyping                  {":typing"} ;
-- position token          TColNegPreconditions        {":negative-preconditions"} ;
-- position token          TColDisjPreconditions       {":disjunctive-preconditions"} ;
-- position token          TColExistPreconditions      {":existential-preconditions"} ;
-- position token          TColUnivPreconditions       {":universal-preconditions"} ;
-- position token          TColConditionalEffects      {":conditional-effects"} ;
-- position token          TColEquality                {":equality"} ;
-- position token          TColModalPreconditions      {":modal-preconditions"} ;
-- position token          TColModalPostconditions     {":modal-postconditions"} ;
-- position token          TColOnticChange             {":ontic-change"} ;
-- position token          TColCommonKnowledge         {":common-knowledge"} ;
-- position token          TColDynamicCommonKnowledge  {":dynamic-common-knowledge"} ;
-- position token          TColFinitaryTheory          {":finitary-theory"} ;
-- position token          TColMaxModalDepth           {":maximum-modal-depth"} ;

--- Actions
-- position token          TOntic                      {"ontic"} ;
-- position token          TSensing                    {"sensing"} ;
-- position token          TAnnouncement               {"announcement"} ;


--- Events and Worlds
-- position token          TIdleEvent                  {"idle-event"} ;

-- position token          TEvent                      {":event"} ;
-- position token          TEvents                     {":events"} ;
-- position token          TRelations                  {":relations"} ;
-- position token          TDesignated                 {":designated"} ;

-- position token          TWorlds                     {":worlds"} ;
-- position token          TValuation                  {":valuation"} ;


--- Formulae
-- position token          TImply                      {"imply"} ;
-- position token          TAnd                        {"and"} ;
-- position token          TOr                         {"or"} ;
-- position token          TNot                        {"not"} ;
-- position token          TLeftSqBracket              '[' ;
-- position token          TRightSqBracket             ']' ;
-- position token          TLeftAngBracket             '<' ;
-- position token          TRightAngBracket            '>' ;
-- position token          TTrue                       {"true"} ;
-- position token          TFalse                      {"false"} ;
-- position token          TEquals                     '=' ;
