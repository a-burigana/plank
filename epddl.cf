---------- LBNF Grammar for EPDDL ----------
--
-- Authors: Alessandro Burigana, Francesco Fabiano
-- License: MIT
--


-- EPDDL Comments
comment                 ";" ;


-- Parsing Entrypoints
entrypoints             MainDef ;

-- Using MainDef because multiple entrypoints are not supported by BNFC cpp backend
-- Domain, action-type library and problem definitions MUST come in different files
Domain.                 MainDef                 ::= DomainDef ;
ActionTypeLibrary.      MainDef                 ::= LibraryDef ;
Problem.                MainDef                 ::= ProblemDef ;



----- EPDDL Domain -----
EPDDLDomain.            DomainDef               ::= "(" "define" "(" "domain" Name ")" [DomainItemDef] ")" ;

EPDDLDomLibrary.        DomainItemDef           ::= DomainLibraryNameDef ;
EPDDLDomRequire.        DomainItemDef           ::= RequireDef ;
EPDDLDomTypes.          DomainItemDef           ::= TypesDef ;
EPDDLDomPredicates.     DomainItemDef           ::= PredicateListDef ;

EPDDLDomObsGroups.      DomainItemDef           ::= ObservabilityGroupsDef ;
EPDDLDomAction.         DomainItemDef           ::= ActionDef ;
separator               DomainItemDef "" ;

-- EPDDLDomRequire.        DeclarationDef          ::= RequireDef ;
-- EPDDLDomTypes.          DeclarationDef          ::= TypesDef ;
-- EPDDLDomPredicates.     DeclarationDef          ::= PredicateListDef ;
-- separator               DeclarationDef "" ;
-- 
-- EPDDLDomAction.         StructureDef            ::= ActionDef ;
-- EPDDLDomActionType.     StructureDef            ::= ActionTypeDef ;
-- EPDDLDomEvent.          StructureDef            ::= EventDef ;
-- separator               StructureDef "" ;


--- EPDDL Domain Library Definition
EPDDLLibraryName.       DomainLibraryNameDef    ::= "(" ":action-type-libraries" [Name] ")" ;


--- EPDDL Requirements Definition
EPDDLRequire.           RequireDef              ::= "(" ":requirements" [RequireKey] ")" ;


--- EPDDL Types Definition
EPDDLTypes.             TypesDef                ::= "(" ":types" TypedIdentList ")" ;


--- EPDDL Predicates Definition
EPDDLPredicateList.     PredicateListDef        ::= "(" ":predicates" [PredicateDef] ")" ;
EPDDLPredicateDef.      PredicateDef            ::= "(" PredicateName BasicTypedVariableList ")" ;
separator nonempty      PredicateDef "" ;


--- EPDDL Observability Groups Definition
EPDDLObsGroupsNames.    ObservabilityGroupsDef  ::= "(" ":observability-groups" [ObservingAgentGroup] ")" ;


--- EPDDL Actions Definition
EPDDLAction.            ActionDef               ::= "(" ":action" ActionName ActionParameterDef ActionSignatureDef ActionPreDef ActionObsDef ")" ;

ActionPar.              ActionParameterDef      ::= ":parameters" "(" BasicTypedVariableList ")" ;

ActionSignature.        ActionSignatureDef      ::= ":action-type" "(" ActionTypeName [Parameter] ")" ;

ActionPre.              ActionPreDef            ::= ":precondition" Precondition ;

FormulaPrecondition.    Precondition            ::= Formula ;
TrivialPrecondition.    Precondition            ::= "(" ")" ;

ActionObs.              ActionObsDef            ::= ":observability-conditions" [ObsConditionDef] ;
EmptyActionObs.         ActionObsDef            ::= ;

-- Observability Conditions 
EmptyObsCond.           ObsConditionDef         ::= "("             ObservingAgent ObservingAgentGroup                ")" ;
ObsCond.                ObsConditionDef         ::= "("             ObservingAgent ObservingAgentGroup "when" Formula ")" ;
ObsOtherwiseCond.       ObsConditionDef         ::= "(" "otherwise" ObservingAgent ObservingAgentGroup                ")" ;
separator nonempty      ObsConditionDef "" ;



----- EPDDL Action Type Library -----
EPDDLLibrary.           LibraryDef              ::= "(" "define" "(" "library" Name ")" [LibraryItemDef] ")" ;

EPDDLLibObsGroups.      LibraryItemDef          ::= ObservabilityGroupsDef ;
EPDDLLibActionType.     LibraryItemDef          ::= ActionTypeDef ;
EPDDLLibEvent.          LibraryItemDef          ::= EventDef ;
separator               LibraryItemDef "" ;


--- EPDDL Action Types Definition
EPDDLActType.           ActionTypeDef           ::= "(" ":action-type" ActionTypeName ActionTypeParameterDef ActionTypeFrameDef ActionTypeEventsDef ActionTypeRelDef ActionTypeDesDef ")" ;

ActTypePar.             ActionTypeParameterDef  ::= ":parameters" "(" TypedVariableList ")" ;

ActTypeFrame.           ActionTypeFrameDef      ::= ":frame-of-reference" "(" [ObservingAgentGroup] ")" ;
EmptyActTypeFrame.      ActionTypeFrameDef      ::= ;

ActTypeEvents.          ActionTypeEventsDef     ::= ":events" [EventSignature] ;
ActTypeRel.             ActionTypeRelDef        ::= ":relations" ActionRelations ;
ActTypeDes.             ActionTypeDesDef        ::= ":designated" "(" [EventName] ")" ;

EventSign.              EventSignature          ::= "(" EventName "(" [Parameter] ")" ")" ;
EventSignNoParam.       EventSignature          ::= "(" EventName ")" ;
separator nonempty      EventSignature "" ;

EventsActionRel.        ActionRelations         ::= [EventRelation] ;
TrivialActionRel.       ActionRelations         ::= "(" ")" ;

EventRel.               EventRelation           ::= "(" AgentList [EventNamePair] ")" ;
TrivialEventRel.        EventRelation           ::= "(" AgentList "(" ")" ")" ;
separator nonempty      EventRelation "" ;

EventPair.              EventNamePair           ::= "(" EventName EventName ")" ;
separator nonempty      EventNamePair "" ;


--- EPDDL Events Definition
EPDDLEvent.             EventDef                ::= "(" ":event" EventName EventParameterDef EventPreDef EventPostDef ")" ;

EventPar.               EventParameterDef       ::= ":parameters" "(" TypedVariableList ")"  ;

EventPre.               EventPreDef             ::= ":precondition" Precondition ;

EventPost.              EventPostDef            ::= ":postconditions" EventPostconditions ;
EmptyEventPost.         EventPostDef            ::= ;

Postconditions.         EventPostconditions     ::= [LiteralPostcondition] ;
TrivialPostconditions.  EventPostconditions     ::= "(" ")" ;
EmptyPostconditions.    EventPostconditions     ::= ;

LiteralPost.            LiteralPostcondition    ::= "(" Literal "when" Postcondition ")" ;
TrivialLiteralPost.     LiteralPostcondition    ::= "(" Literal ")" ;
separator nonempty      LiteralPostcondition "" ;

FormulaPostcondition.   Postcondition           ::= Formula ;
TrivialPostcondition.   Postcondition           ::= "(" ")" ;



----- EPDDL Problem -----
EPDDLProblem.           ProblemDef              ::= "(" "define" "(" "problem" Name ")" [ProblemItemDef] ")" ;

EPDDLProbDomain.        ProblemItemDef          ::= ProblemDomainNameDef ;
EPDDLProbRequire.       ProblemItemDef          ::= RequireDef ;
EPDDLProbAgents.        ProblemItemDef          ::= AgentNamesDef ;
EPDDLProbAgentGroups.   ProblemItemDef          ::= AgentGroupsListDef ;
EPDDLProbObjects.       ProblemItemDef          ::= ObjectNamesDef ;
EPDDLProbInit.          ProblemItemDef          ::= InitDef ;
EPDDLProbInitModel.     ProblemItemDef          ::= InitialModelDef ;
EPDDLProbGoal.          ProblemItemDef          ::= GoalDef ;
separator               ProblemItemDef "" ;


--- EPDDL Problem Domain Definition
EPDDLDomainName.        ProblemDomainNameDef    ::= "(" ":domain" Name ")" ;


--- EPDDL Agents Definition
EPDDLAgentNames.        AgentNamesDef           ::= "(" ":agents" [AgentName] ")" ;

--- EPDDL Agent Groups Definition
EPDDLAgentGroupsList.   AgentGroupsListDef      ::= "(" ":agent-groups" [AgentGroupDef] ")" ;

EPDDLAgentGroupDef.     AgentGroupDef           ::= "(" "[" [AgentName] "]" "as" AgentGroupName ")" ;
separator               AgentGroupDef "" ;


--- EPDDL Objects Definition
EPDDLObjectNames.       ObjectNamesDef          ::= "(" ":objects" TypedIdentList ")" ;


--- EPDDL Initial State Definition
EPDDLInitialState.      InitDef                 ::= "(" ":init" InitialStateDescr ")" ;

FinitaryTheoryDescr.    InitialStateDescr       ::= [FTheoryFormula] ;
InitialModelDescr.      InitialStateDescr       ::= "(" ":model" ModelName ")" ;

-- Finitary Theory
FTheoryPredForm.        FTheoryFormula          ::= PredicateFormula ;
FTheoryCKPredForm.      FTheoryFormula          ::= "[" AllAgents "]" PredicateFormula ;
FTheoryCKKPredForm.     FTheoryFormula          ::= "[" AllAgents "]" KPredicateFormula ;
FTheoryCKOrKPredForm.   FTheoryFormula          ::= "[" AllAgents "]" OrKPredicateFormula ;
FTheoryCKAndKPredForm.  FTheoryFormula          ::= "[" AllAgents "]" AndKPredicateFormula ;
_.                      FTheoryFormula          ::= "(" FTheoryFormula ")" ;
separator nonempty      FTheoryFormula "" ;

KPredFormula.           KPredicateFormula       ::= "[" AgentName "]" PredicateFormula ;
_.                      KPredicateFormula       ::= "(" KPredicateFormula ")" ;

KWPredFormula.          OrKPredicateFormula     ::= KnowsWhether PredicateFormula ;
OrKPredFormula.         OrKPredicateFormula     ::= "(" "or" KPredicateFormula KPredicateFormula ")" ;
_.                      OrKPredicateFormula     ::= "(" OrKPredicateFormula ")" ;

NotKWPredFormula.       OrKPredicateFormula     ::= "(" "not" KnowsWhether PredicateFormula ")" ;
AndKPredFormula.        AndKPredicateFormula    ::= "(" "and" NotKPredicateFormula NotKPredicateFormula ")" ;
_.                      AndKPredicateFormula    ::= "(" AndKPredicateFormula ")" ;

NotKPredFormula.        NotKPredicateFormula    ::= "(" "not" KPredicateFormula ")" ;
_.                      NotKPredicateFormula    ::= "(" NotKPredicateFormula ")" ;


--- EPDDL Initial Model
EPDDLInitialModel.      InitialModelDef         ::= "(" ":model" ModelName ModelWorldsDef ModelRelDef ModelValDef ModelDesDef ")" ;

ModelWorlds.            ModelWorldsDef          ::= ":worlds" "(" [WorldName] ")" ;
ModelRel.               ModelRelDef             ::= ":relations" ModelRelations ;
ModelVal.               ModelValDef             ::= ":valuation" ModelValuation ;
ModelDes.               ModelDesDef             ::= ":designated" "(" [WorldName] ")" ;

WorldsModelRel.         ModelRelations          ::= [WorldRelation] ;
TrivialModelRel.        ModelRelations          ::= "(" ")" ;

WorldsModelVal.         ModelValuation          ::= [WorldValuation] ;
TrivialModelVal.        ModelValuation          ::= "(" ")" ;

WorldRel.               WorldRelation           ::= "(" AgentList [WorldNamePair] ")" ;
TrivialWorldRel.        WorldRelation           ::= "(" AgentList "(" ")" ")" ;
separator nonempty      WorldRelation "" ;

WorldPair.              WorldNamePair           ::= "(" WorldName WorldName ")" ;
separator nonempty      WorldNamePair "" ;

WorldVal.               WorldValuation          ::= "(" WorldName "[" [Literal] "]" ")" ;
separator nonempty      WorldValuation "" ;


--- EPDDL Goal Definition
EPDDLGoals.             GoalDef                 ::= "(" ":goal" Formula ")" ;



----- EPDDL Formulae -----
ImplyFormula.           Formula                 ::= "(" "imply"          Formula  Formula  ")" ;
OrFormula.              Formula                 ::= "(" "or"             Formula [Formula] ")" ;
AndFormula.             Formula                 ::= "(" "and"            Formula [Formula] ")" ;
NotFormula.             Formula                 ::= "(" "not"            Formula           ")" ;

ModalFormula.           Formula                 ::= Modality Formula ;

AtmFormula.             Formula                 ::= AtomicFormula ;
AtmEqFormula.           Formula                 ::= AtomicEqFormula ;
TrueFormula.            Formula                 ::= "true" ;
FalseFormula.           Formula                 ::= "false" ;
_.                      Formula                 ::= "(" Formula ")" ;
separator nonempty      Formula "" ;

Predicate.              AtomicFormula           ::= "(" PredicateName [MetaTerm] ")" ;
VarFormula.             AtomicFormula           ::= "(" Variable ")" ;
EqFormula.              AtomicEqFormula         ::= "(" "=" Term Term ")" ;


--- EPDDL Terms
EPDDLMetaTerm.          MetaTerm                ::= Term ;
EPDDLMetaTermAnonVar.   MetaTerm                ::= AnonVarAgent ;
separator               MetaTerm "" ;  

EPDDLTermName.          Term                    ::= Name ;
EPDDLTermAgentName.     Term                    ::= AgentName ;
EPDDLTermVar.           Term                    ::= Variable ;


--- EPDDL Modalities
EPDDLLabeledMod.        Modality                ::= SingleModality ;
EPDDLLabeledGroupMod.   Modality                ::= GroupModality ;

LabBoxMod.              SingleModality          ::= "[" ModalityLabel ModalityAgent "]" ;
LabDiamondMod.          SingleModality          ::= "<" ModalityLabel ModalityAgent ">" ;

LabBoxGroupMod.         GroupModality           ::= "[" ModalityLabel ModalityAgentGroup "]" ;
LabDiamondGroupMod.     GroupModality           ::= "<" ModalityLabel ModalityAgentGroup ">" ;

ModLabel.               ModalityLabel           ::= ModalityName ;
KnowsWhetherLabel.      ModalityLabel           ::= "kw." ;
EverybodyKnowsLabel.    ModalityLabel           ::= "e." ;
SomeoneKnowsLabel.      ModalityLabel           ::= "s." ;
EmptyModLabel.          ModalityLabel           ::= ;

-- Used in Finitary Theories definition
KnowsWhetherMod.        KnowsWhether            ::= "[" "kw." AgentName "]" ;


--- Literals
PosLiteral.             Literal                 ::= PositiveLiteral ;
NegLiteral.             Literal                 ::= "(" "not" PositiveLiteral ")" ;
separator               Literal "" ;

PosLiteralAtm.          PositiveLiteral         ::= AtomicFormula ;
PosLiteralVar.          PositiveLiteral         ::= Variable ;


--- Predicate Formulae
ImplyPredFormula.       PredicateFormula        ::= "(" "imply"  PredicateFormula  PredicateFormula  ")" ;
OrPredFormula.          PredicateFormula        ::= "(" "or"     PredicateFormula [PredicateFormula] ")" ;
AndPredFormula.         PredicateFormula        ::= "(" "and"    PredicateFormula [PredicateFormula] ")" ;
NotPredFormula.         PredicateFormula        ::= "(" "not"    PredicateFormula                    ")" ;

LitPredFormula.         PredicateFormula        ::= "(" PredicateName [Name] ")" ;
separator nonempty      PredicateFormula "" ;



----- Basic EPDDL Elements -----

-- Typed Name List
IdList.                 TypedIdentList          ::= [Name] ;
TypedIdList.            TypedIdentList          ::= Name [Name] "-" BasicType TypedIdentList ;


-- Typed Variable List
BasicVarList.           BasicTypedVariableList  ::= [Variable] ;
BasicTypedVarList.      BasicTypedVariableList  ::= Variable [Variable] "-" BasicType BasicTypedVariableList ;

VarList.                TypedVariableList       ::= [Variable] ;
TypedVarList.           TypedVariableList       ::= Variable [Variable] "-" Type TypedVariableList ;


--- EPDDL Predicates
EPDDLPredicate.         PredicateName           ::= Name ;


-- EPDDL Agents
EPDDLModAgent.          ModalityAgent           ::= AgentName ;
EPDDLModVarAgent.       ModalityAgent           ::= Variable ;
EPDDLModAnonVarAgent.   ModalityAgent           ::= AnonVarAgent ;
EPDDLModAllAgents.      ModalityAgent           ::= AllAgents ;
separator nonempty      ModalityAgent "" ;

EPDDLModAgList.         ModalityAgentGroup      ::= ModalityAgent [ModalityAgent] ;

EPDDLAgentGroup.        AgentGroupName          ::= AgentName ;
EPDDLAllAgentsGroup.    AgentGroupName          ::= AllAgents ;

EPDDLObsAgentGroup.     ObservingAgentGroup     ::= AgentName ;
separator nonempty      ObservingAgentGroup "" ;

EPDDLObsAgent.          ObservingAgent          ::= AgentName ;
EPDDLObsVarAgent.       ObservingAgent          ::= Variable ;
EPDDLObsAnonVarAgent.   ObservingAgent          ::= AnonVarAgent ;

EPDDLSingleAgentName.   AgentList               ::= AgentName ;
EPDDLAgentNameList.     AgentList               ::= "[" [AgentName] "]" ;
EPDDLAllAgentsList.     AgentList               ::= AllAgents ;

EPDDLAllAgents.         AllAgents               ::= "All" ;
EPDDLAnonVarAgent.      AnonVarAgent            ::= "?_" ;


--- EPDDL Parameters
EPDDLBasicParam.        Parameter               ::= BasicParameter ;
EPDDLListParam.         Parameter               ::= "[" [Formula] "]" ;
separator               Parameter "" ;

EPDDLTermParam.         BasicParameter          ::= Term ;
EPDDLFormulaParam.      BasicParameter          ::= Formula ;
EPDDLTrivialParam.      BasicParameter          ::= "(" ")" ;


--- EPDDL Types
EPDDLBasicType.         Type                    ::= BasicType ;
EPDDLCompoundType.      Type                    ::= CompoundType ;

EPDDLBasicTypeName.     BasicType               ::= Name ;
EPDDLBasicResType.      BasicType               ::= ReservedBasicType ;

EPDDLFormulaListType.   CompoundType            ::= "[" ReservedFormulaType "]" ;

ResFormulaType.         ReservedBasicType       ::= ReservedFormulaType ;
ResAgentType.           ReservedBasicType       ::= "agent" ;

PredicateType.          ReservedFormulaType     ::= "predicate" ;
LiteralType.            ReservedFormulaType     ::= "literal" ;
PredFormulaType.        ReservedFormulaType     ::= "predicate-formula" ;
FormulaType.            ReservedFormulaType     ::= "formula" ;


--- EPDDL Events
EPDDLResEventName.      EventName               ::= ReservedEventName ;
EPDDLEventName.         EventName               ::= Name ;
separator nonempty      EventName "" ;

IdleEvent.              ReservedEventName       ::= "idle-event" ;


--- EPDDL Action Types
EPDDLResActTypeName.    ActionTypeName          ::= ReservedActionTypeName ;
EPDDLActTypeName.       ActionTypeName          ::= Name ;

OntActTypeName.         ReservedActionTypeName  ::= "ontic" ;
SenActTypeName.         ReservedActionTypeName  ::= "sensing" ;
AnnActTypeName.         ReservedActionTypeName  ::= "announcement" ;


--- EPDDL Actions
EPDDLActionName.        ActionName              ::= Name ;


--- EPDDL Models
EPDDLModelName.         ModelName               ::= Name ;
EPDDLWorldName.         WorldName               ::= Name ;
separator nonempty      WorldName "" ;


--- EPDDL Requirements
EPDDLReqDel.            RequireKey              ::= ":del" ;
EPDDLReqTyping.         RequireKey              ::= ":typing" ;
EPDDLReqEquality.       RequireKey              ::= ":equality" ;
EPDDLReqParamList.      RequireKey              ::= ":parameter-lists" ;

EPDDLReqNegPre.         RequireKey              ::= ":negative-preconditions" ;
EPDDLReqDisPre.         RequireKey              ::= ":disjunctive-preconditions" ;
EPDDLReqExiPre.         RequireKey              ::= ":existential-preconditions" ;
EPDDLReqUniPre.         RequireKey              ::= ":universal-preconditions" ;

EPDDLReqModPre.         RequireKey              ::= ":modal-preconditions" ;
EPDDLReqModPost.        RequireKey              ::= ":modal-postconditions" ;

EPDDLReqOnticChange.    RequireKey              ::= ":ontic-change" ;

EPDDLReqCK.             RequireKey              ::= ":common-knowledge" ;
EPDDLReqDynCK.          RequireKey              ::= ":dynamic-common-knowledge" ;

EPDDLReqMAStar.         RequireKey              ::= ":ma-star" ;        -- Group requirement: :parameter-lists, :ma-star-ontic, :ma-star-sensing, :ma-star-announcement, :ma-star-finitary-theory
EPDDLReqOntic.          RequireKey              ::= ":ma-star-ontic" ;
EPDDLReqSensing.        RequireKey              ::= ":ma-star-sensing" ;
EPDDLReqAnnouncement.   RequireKey              ::= ":ma-star-announcement" ;
EPDDLReqFTheory.        RequireKey              ::= ":ma-star-finitary-theory" ;

EPDDLReqMaxPreDepth.    RequireKey              ::= "(" ":maximum-preconditions-depth" Integer ")" ;
EPDDLReqMaxPostDepth.   RequireKey              ::= "(" ":maximum-postconditions-depth" Integer ")" ;
EPDDLReqMaxDepth.       RequireKey              ::= "(" ":maximum-modal-depth" Integer ")" ;
separator nonempty      RequireKey "" ;
-- todo: trova paper con teorema su epistemic actions con propositional preconditions



----- Tokens -----

--- Names
position token          Name                        (lower (letter|digit|'_'|'\'')*) ;
separator               Name "" ;

position token          AgentName                   (upper (letter|digit|'_')*) ;
separator nonempty      AgentName "" ;

position token          ModalityName                (lower (letter|digit|'_'|'\'')* '.') ;

position token          Variable                    ('?' (letter (letter|digit|'_'|'\'')*)) ;
separator               Variable "" ;

-- position token          FormulaVariable             ({"f?"} (letter (letter|digit|'_'|'\'')*)) ;
-- separator               FormulaVariable "" ;

-- position token          FormalParameter             (('?'|{"f?"}) (letter (letter|digit|'_'|'\'')*)) ;
-- separator               FormalParameter "" ;

--- Parentheses
-- position token          TLeftParen                  '(' ;
-- position token          TRightParen                 ')' ;


--- Other Reserved Symbols
-- position token          TDash                       '-' ;
-- position token          TQuestionMark               '?' ;
-- position token          TAnon                       '_' ;


--- Main Domain Keywords
-- position token          TDefine                     {"define"} ;
-- position token          TDomain                     {"domain"} ;
-- position token          TProblem                    {"problem"} ;

-- position token          TColRequirements            {":requirements"} ;
-- position token          TColTypes                   {":types"} ;
-- position token          TColPredicates              {":predicates"} ;
-- position token          TColAction                  {":action"} ;
-- position token          TColParameters              {":parameters"} ;
-- position token          TColActionType              {":action-type"} ;

-- position token          TColDomain                  {":domain"} ;
-- position token          TColModel                   {":model"} ;

-- position token          TColPrecondition            {":precondition"} ;
-- position token          TColPostconditions          {":postconditions"} ;
-- position token          TColObsConditions           {":observability-conditions"} ;
-- position token          TColFrameOfReference        {":frame-of-reference"} ;

-- position token          TOtherwise                  {"otherwise"} ;
-- position token          TAnonAgentVar               {"?_"} ;


--- Main Problem Keywords
-- position token          TColAgents                  {":agents"} ;
-- position token          TColObjects                 {":objects"} ;

-- position token          TColInit                    {":init"} ;
-- position token          TColGoal                    {":goal"} ;


--- Types
-- position token          TAgentType                  {"agent"} ;
-- position token          TObjectType                 {"object"} ;
-- position token          TPredicateType              {"predicate"} ;
-- position token          TLiteralType                {"literal"} ;
-- position token          TPredFormulaType            {"predicate-formula"} ;
-- position token          TFormulaType                {"formula"} ;


--- Requirements
-- position token          TColDel                     {":del"} ;
-- position token          TColTyping                  {":typing"} ;
-- position token          TColNegPreconditions        {":negative-preconditions"} ;
-- position token          TColDisjPreconditions       {":disjunctive-preconditions"} ;
-- position token          TColExistPreconditions      {":existential-preconditions"} ;
-- position token          TColUnivPreconditions       {":universal-preconditions"} ;
-- position token          TColConditionalEffects      {":conditional-effects"} ;
-- position token          TColEquality                {":equality"} ;
-- position token          TColModalPreconditions      {":modal-preconditions"} ;
-- position token          TColModalPostconditions     {":modal-postconditions"} ;
-- position token          TColOnticChange             {":ontic-change"} ;
-- position token          TColCommonKnowledge         {":common-knowledge"} ;
-- position token          TColDynamicCommonKnowledge  {":dynamic-common-knowledge"} ;
-- position token          TColFinitaryTheory          {":finitary-theory"} ;
-- position token          TColMaxModalDepth           {":maximum-modal-depth"} ;

--- Actions
-- position token          TOntic                      {"ontic"} ;
-- position token          TSensing                    {"sensing"} ;
-- position token          TAnnouncement               {"announcement"} ;


--- Events and Worlds
-- position token          TIdleEvent                  {"idle-event"} ;

-- position token          TEvent                      {":event"} ;
-- position token          TEvents                     {":events"} ;
-- position token          TRelations                  {":relations"} ;
-- position token          TDesignated                 {":designated"} ;

-- position token          TWorlds                     {":worlds"} ;
-- position token          TValuation                  {":valuation"} ;


--- Formulae
-- position token          TImply                      {"imply"} ;
-- position token          TAnd                        {"and"} ;
-- position token          TOr                         {"or"} ;
-- position token          TNot                        {"not"} ;
-- position token          TLeftSqBracket              '[' ;
-- position token          TRightSqBracket             ']' ;
-- position token          TLeftAngBracket             '<' ;
-- position token          TRightAngBracket            '>' ;
-- position token          TTrue                       {"true"} ;
-- position token          TFalse                      {"false"} ;
-- position token          TEquals                     '=' ;
